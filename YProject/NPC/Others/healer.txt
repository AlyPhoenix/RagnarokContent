/////////////////////////////////////////////
//NPC Créé par Lyyn - lyyn@bewoopi.com
//Pour Tradition Online
//////--------------------------------------
//Date de création :
// 24 juillet 2009
//////--------------------------------------
//Description du script :
//Healer original avec système de fatigue.
//////--------------------------------------
//Suivi des modifications :
//-------------------------
//06-07-2014	:	Suppression des prompt + remaniement du code
//24-07-2009	:	Création du NPC
////////////////////////////////////////////

function	script	F_recup	{
	
	// CONFIGURATION
	.msRecup = 1;		// Toutes les X millisecondes le healer récupère 1 point de fatigue [Défaut : 5500]
	
	set $@regen[getarg(0)],1;
	set $@fatigue[getarg(0)],0;
	goto L_regen;
	end;
	
	L_regen:
		while($@regen[getarg(0)]){
			if ($@fatigue[getarg(0)] == 0){
				sleep 5000; // vérification toutes les 5 secondes
				continue;
			}
			else {
				sleep .msRecup; // 1p/30sec
				set $@fatigue[getarg(0)],$@fatigue[getarg(0)]-1;
				continue;
			}
		}
} // function

function	script	F_fatigue	{
	set @med_occup,0;
	mes "[Doctor]";
	mes "Sorry, I'm too exhausted to heal. Sorry.";
	close;
}


sec_in02,136,162,5	script	Doctor#tradition	121,{
	callfunc "F_menuheal",0;
	
	OnInit:
		callfunc "F_recup",0;
		end;
} // npc de base o/

prontera,151,190,5	script	Doctor#prontera	121,{
	callfunc "F_menuheal",1;
	
	OnInit:
		callfunc "F_recup",1;
		end;
}

alberta,37,240,3	script	Doctor#alberta	121,{
	callfunc "F_menuheal",2;
	
	OnInit:
		callfunc "F_recup",2;
		end;
}
aldebaran,135,119,5	script	Doctor#aldebaran	121,{
	callfunc "F_menuheal",3;
	
	OnInit:
		callfunc "F_recup",3;
		end;
}

amatsu,201,87,3	script	Doctor#amatsu	121,{
	callfunc "F_menuheal",4;
	
	OnInit:
		callfunc "F_recup",4;
		end;
}

ayothaya,155,117,3	script	Doctor#ayothaya	121,{
	callfunc "F_menuheal",5;
	
	OnInit:
		callfunc "F_recup",5;
		end;
}

comodo,182,154,5	script	Doctor#comodo	121,{
	callfunc "F_menuheal",6;
	
	OnInit:
		callfunc "F_recup",6;
		end;
}

einbroch,70,204,3	script	Doctor#einbroch	121,{
	callfunc "F_menuheal",7;
	
	OnInit:
		callfunc "F_recup",7;
		end;
}

einbech,173,136,5	script	Doctor#einbech	121,{
	callfunc "F_menuheal",8;
	
	OnInit:
		callfunc "F_recup",8;
		end;
}

geffen,124,74,3	script	Doctor#geffen	121,{
	callfunc "F_menuheal",9;
	
	OnInit:
		callfunc "F_recup",9;
		end;
}

gonryun,157,126,3	script	Doctor#gonryun	121,{
	callfunc "F_menuheal",10;
	
	OnInit:
		callfunc "F_recup",10;
		end;
}

hugel,102,151,3	script	Doctor#hugel	121,{
	callfunc "F_menuheal",11;
	
	OnInit:
		callfunc "F_recup",11;
		end;
}
izlude,123,116,3	script	Doctor#izlude	121,{
	callfunc "F_menuheal",13;
	
	OnInit:
		callfunc "F_recup",13;
		end;
}

lighthalzen,167,105,5	script	Doctor#lighthalzen	121,{
	callfunc "F_menuheal",14;
	
	OnInit:
		callfunc "F_recup",14;
		end;
}

louyang,209,99,5	script	Doctor#louyang	121,{
	callfunc "F_menuheal",15;
	
	OnInit:
		callfunc "F_recup",15;
		end;
}

xmas,142,137,3	script	Doctor#lutie	121,{
	callfunc "F_menuheal",16;
	
	OnInit:
		callfunc "F_recup",16;
		end;
}

morocc,151,100,5	script	Doctor#morocc	121,{
	callfunc "F_menuheal",17;
	
	OnInit:
		callfunc "F_recup",17;
		end;
}

payon,155,99,5	script	Doctor#payon	121,{
	callfunc "F_menuheal",18;
	
	OnInit:
		callfunc "F_recup",18;
		end;
}

umbala,131,140,5	script	Doctor#umbala	121,{
	callfunc "F_menuheal",19;
	
	OnInit:
		callfunc "F_recup",19;
		end;
}
yuno,165,189,5	script	Doctor#yuno	121,{
	callfunc "F_menuheal",20;

	OnInit:
		callfunc "F_recup",20;
		end;
}
jawaii,210,219,5	script	Doctor#jawaii	121,{
	callfunc "F_menuheal",21;

	OnInit:
		callfunc "F_recup",21;
		end;
}
/*
moscovia,219,199,5	script	Doctor#moscovia	121,{
	callfunc "F_menuheal",22;

	OnInit:
		callfunc "F_recup",22;
		end;
}
*/
rachel,114,141,3	script	Doctor#rachel	121,{
	callfunc "F_menuheal",23;

	OnInit:
		callfunc "F_recup",23;
		end;
}
veins,205,120,3	script	Doctor#veins	121,{
	callfunc "F_menuheal",24;

	OnInit:
		callfunc "F_recup",24;
		end;
}

/*
Edoras,123,87,3	script	Doctor#edoras	121,{
	callfunc "F_menuheal",25;

	OnInit:
		callfunc "F_recup",25;
		end;
}
*/

//
// donjons
//

/*
niflheim,28,161,5	script	Doctor#niflheim	121,{
	callfunc "F_menuheal",25;

	OnInit:
		callfunc "F_recup",25;
		end;
}
pay_arche,42,139,3	script	Doctor#payondun	121,{
	callfunc "F_menuheal",26;

	OnInit:
		callfunc "F_recup",26;
		end;
}
in_orcs01,37,173,5	script	Doctor#orcdun	121,{
	callfunc "F_menuheal",27;

	OnInit:
		callfunc "F_recup",27;
		end;
}
*/

// La fonction.

function	script	F_menuheal	{


// CONFIGURATION
	set .debug,0;			// Affichage des messages de debug
	set .parler,1;			// Décider si le npc parle. (npctalk)
	set .gmlevel,10;		// Détermine à partir de quel gm level on peut voir la fatigue du healer
	set .fatiguemax,100;	// valeur max de fatigue
	set .healhp,3;			// fatigue ajoutée à chaque heal HP only (utilisé si .onlyHP == 1)
	set .healsp,2;			// Pareil pour les SP
	set .healtotal,5;		// Pareil pour un soin total (HP + SP)
	set .variation,5;		// % de variation sur les heals
	set .onlyHP,0;			// Si 1 : le healer ne propose pas de soin SP
	set .cooldown,0;		// Temps de blocage personnel sur le healer (en secondes)
	set .noHealWOE,0;		// Si 1, pas de heal en WOE.
	set .FUCKINGLEVELTEMPORAIREDEMESCOUILLES, 99;	// BITCH
// CONFIGURATION [Fin]
	
	// Est-on GM ?
	.@isGM = (getgmlevel() >= .gmlevel) ? 1 : 0;
	
	if(.noHealWOE) {
		if(agitcheck() || agitcheck2()) {
			mes "^919191The Doctor looks somewhere else. He doesn't even notice you.^000000";
			close;
		}
	}
	
	// Protection anti-spam (cf. Asura Strike)	
	if((gettimetick(2) - stampHeal) < .cooldown) {
		if(.debug) dispbottom stampHeal + " ~ " + (gettimetick(2) - stampHeal);
		mes "[Doctor]";
		mes "Hey, " + strcharinfo(0) + ". Do not use my powers too much !";
		mes "Come back later.";
		if (.@isGM) mes "^919191Fatigue : "+$@fatigue[getarg(0)]+" / " + .fatiguemax;
		close;
	}

	if (@med_occup == 1) {
		if(.debug) message strcharinfo(0), "@med_occup = 1 !";
		end;
	}
	
	mes "[Doctor]";

	if($@fatigue[getarg(0)] <= 20) {
		mes "I'm full of energy right now !";
		mes "What can I do for you ?";
	}
	else if($@fatigue[getarg(0)] <= 40) {
		mes "Isn't it a bit warm today? Arf...";
		mes "Need help ?";
	}
	else if($@fatigue[getarg(0)] <= 75) {
		mes "^919191*Yawn*^000000";
		mes "Sorry...I'm a bit tired...";
		mes "What is it ?";
	}
	else {
		mes "I... Water... Arrrhhh !";
		mes "";
		mes "^919191The Doctor really looks tired...^000000";
	}

	if (.@isGM) mes "Fatigue : "+$@fatigue[getarg(0)]+" / " + .fatiguemax;
	next;
	
	switch(select("I need heal.","Nothing.")){
		case 1:
			if ($@fatigue[getarg(0)] >= .fatiguemax) {
				mes "[Doctor]";
				mes "I'm too tired right now. Wait a moment please...";
				close;
			}
			if ((!.onlyHP && Hp == MaxHp && Sp == MaxSp) || (.onlyHP && Hp == MaxHp)) {
				mes "[Doctor]";
				mes "You looks like in full health..";
				close;
			}
			
			// définition du % de heal
			set .@fatigue,$@fatigue[getarg(0)]*(100 / .fatiguemax);	// Fatigue du Doctor remise sur une base de 100
			set .@randmax,100 - .@fatigue;	// Max % de heal
			
			// Valeur minimale du heal
			set .@randmin, ((.@randmax - .variation) >= 0) ? .@randmax - .variation : 0;
			
			set .@pourcentheal,rand(.@randmin,.@randmax);
			if(.debug) message strcharinfo(0),"[Healer Debug] @fatigue : " + .@fatigue + " | @randmax : " + .@randmax + " | @randmin : " + .@randmin + " | % heal : " + .@pourcentheal;
			
			mes "[Doctor]";
			
			if(!.onlyHP) mes "What kind of heal ?";
			else mes "Do you really wanna use my healing power ?";
			
			if (.@isGM) mes "% de heal : "+.@pourcentheal;
			next;
			
			.@tempResult = 0;
			if(!.onlyHP) {
				.@soinhp$  = (Hp < MaxHp) ? "Health." : "";
				.@soinsp$  = (Sp < MaxSp && BaseLevel <= .FUCKINGLEVELTEMPORAIREDEMESCOUILLES) ? "Spiritual." : "";
				.@sointot$ = (Hp < MaxHp && Sp < MaxSp && BaseLevel <= .FUCKINGLEVELTEMPORAIREDEMESCOUILLES) ? "Full Heal." : "";
			}
			else {
				.@soinhp$ = "Oui !";
			}
			
			.@typeSoin = select(.@sointot$,.@soinhp$,.@soinsp$, "Goodbye.");
			if(.debug) message strcharinfo(0),"[Healer Debug] @typeSoin (menu) = " + .@typeSoin;

			//	.@verif = dans quel état de fatigue sera le healer après les soins
			//	si .@verif dépasse fatigue max, le Doctor refusera de soigner.
			//  .@tempType retient le type de soin (la valeur de fatigue à ajouter au soin s'il est lancé)
			switch(.@typeSoin) {
				case 1:
					.@verif = $@fatigue[getarg(0)] + .healtotal;
					.@tempType = .healtotal;
					.@idEffect = 325;	// Heal
					break;
				case 2:
					.@verif = $@fatigue[getarg(0)] + .healhp;
					.@tempType = .healhp;
					.@idEffect = 325;	// Heal
					break;
				case 3:
					.@verif = $@fatigue[getarg(0)] + .healsp;
					.@tempType = .healsp;
					.@idEffect = 14;	// Une aura bleue
					break;
				case 4:
					mes "[Doctor]";
					mes "See you soon !";
					close;
			}
			
			if(.debug) message strcharinfo(0), "[Healer Debug] .@verif : " + .@verif + " | .fatiguemax : " + .fatiguemax;
			if(.@verif > .fatiguemax) {
				// Plus assez d'énergie pour lancer le soin
				callfunc "F_fatigue";
			}
			else {
				mes "[Doctor]";
				mes "Allright. Here we go !";
				close2;
				
				@med_occup = 1;		// Antiflood personnel
				$@fatigue[getarg(0)] += .@tempType;		// .@tempType = valeur du soin choisi
				
				if(.parler) npctalk "Ahh...";
				specialeffect 58;
				specialeffect 546;
				sleep2 2000;
				
				if(.parler) npctalk (.@typeSoin == 2) ? "Spiritual Heal !" : "Heal !";
				specialeffect2 .@idEffect;
				
				// Soin effectif
				switch(.@typeSoin) {
					case 1:
						percentheal .@pourcentheal,.@pourcentheal;
						break;
					case 2:
						percentheal .@pourcentheal,0;
						break;
					case 3:
						percentheal 0,.@pourcentheal;
				}
				stampHeal = gettimetick(2);	// timestamp classique
				
				// Fin du soin, on vide les variables
			}
			
			// Fin du blocage antiflood
			@med_occup = 0;
			end; // fin des soins

		case 2:
			mes "[Doctor]";
			mes "Good luck, see you.";
			close;
	}
} // function