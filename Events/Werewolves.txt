//Marin qui va les warp à l'event
prt_vilg02,53,145,5	script	Thiercelieux Sailor	709,{

	set .@npc_marin$, "^0000FF[Sailor]^000000";
	
	mes .@npc_marin$;
	mes "Hello " + strcharinfo(0) + " !";
	if(getgmlevel() >= 60)
	{
		mes "Oh, a GM. What can I do for you ?";
		next;
		switch(select("Be warped:Start the event:Nothing, thanks"))
		{
			case 1:
				warp "thier_vlg",55,55; //Changer le warp pour mettre le nom de la map du loup-garou
				close;
				
			case 2:
				donpcevent "evtlg::OnStartLg";
				close;
				
			case 3:
				mes .@npc_marin$;
				mes "No problem, see you soon !";
				close;
		}
	}
	mes "Do you wanna go to the Thiercellieux island ?";
	next;
	if(select("Yes please:No thanks") == 2)
	{
		mes .@npc_marin$;
		mes "Allright, see you soon.";
		close;
	}
	warp "thier_vlg",55,55; //Changer le warp pour mettre le nom de la map du loup-garou
	close;
}

//Spirit, le maître du jeu
thier_vlg,62,64,5	script	Spirit	802,{

	set .@npc_spirit$, "^0000FF[Spirit]^000000"; //Nom npc
	
	mes .@npc_spirit$;
	mes "Hello " + strcharinfo(0) + " !";
	if(getgmlevel() >= 60)
	{
		mes "Oh, a GM. What can I do for you ?";
		switch(select("Player mode:Start the event:Nothing at all"))
		{
			case 1:
				break;
				
			case 2:
				donpcevent "evtlg::OnStartLg";
				close;
			
			case 3:
				mes "Okay, see you soon !";
				close;
		}
	}
	//Regarde si c'est les inscriptions
	if($event_ins_lg == 1)
	{
		if($joueur_lg < 1)
			set $joueur_lg, 0;
		//Test si joueur est ds le tableau de joueur
		set .@village_lg$,"";
		query_sql("SELECT pseudo FROM loup_garou",.@village_lg$);
		for(set .@lg_joueurs, 0 ; .@lg_joueurs < getarraysize(.@village_lg$) ; set .@lg_joueurs, .@lg_joueurs + 1)
			if(getelementofarray(.@village_lg$,.@lg_joueurs) == strcharinfo(0))
			{
				mes "You're already registered !";
				close;
			}
		mes "Do you want to be part of the Thiercellieux Council ?";
		next;
		switch(select("Yes please:No I'm just here to observe:Sorry...My mystake"))
		{
			case 1:
				mes .@npc_spirit$;
				mes "Okay. It'll cost ^0000FF10 000z^000000 to get a place.";
				next;
				if(Zeny < 10000)
				{
					mes .@npc_spirit$;
					mes "Sorry but you lack some money.";
					mes "Come back with ^0000FF10 000z^000000.";
					close;
				}
				if($event_ins_lg == 0)
				{
					mes .@npc_spirit$;
					mes "You were too long...";
					close;
				}
				set Zeny, Zeny - 10000; //Enlève la somme de zeny au joueur
				query_sql ("INSERT INTO `loup_garou` VALUES ('', '" + strcharinfo(0) + "', '', '', '', 0, 0, '', 0, 0, 0)"); //Ajoute le joueur dans la bdd
				mes .@npc_spirit$;
				mes "Perfect, you're registered.";
				next;
				if($joueur_lg == $max_hab - 1)
				{
					set $event_ins_lg, 0; //Met fin aux inscriptions
					set $event_lancement_lg, 1; //Active la partie
				}
				switch($joueur_lg)//positionne le joueur sur son arbre
				{
					case 0:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",57,62;
						close;
						
					case 1:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",57,66;
						close;
					
					case 2:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",59,68;
						close;
					
					case 3:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",63,68;
						close;
				
					case 4:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",66,66;
						close;
					
					case 5:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",67,63;
						close;
					
					case 6:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",66,60;
						close;
					
					case 7:
						set $joueur_lg, $joueur_lg + 1;
						warp "thier_vlg",63,59;
						close;
				}
		
			case 2:
				mes .@npc_spirit$;
				mes "Perfect, your can sit around here.";
				close;
				
			case 3:
				mes .@npc_spirit$;
				mes "No problem. See you soon.";
				warp "save",0,0;
				close;
		}
	}//Fin du test d'inscriptions
	if($event_lancement_lg == 1)
	{
		set .@joueur_en_jeu_lg, 0;
		//Test si joueur est ds la partie
		set .@village_lg$,"";
		query_sql("SELECT pseudo FROM loup_garou",.@village_lg$);
		for(set .@lg_joueurs, 0 ; .@lg_joueurs < getarraysize(.@village_lg$) ; set .@lg_joueurs, .@lg_joueurs + 1)
			if(getelementofarray(.@village_lg$,.@lg_joueurs) == strcharinfo(0))
				set .@joueur_en_jeu_lg,1;
		if(.@joueur_en_jeu_lg == 0)
		{
			mes "You're not part of the village !";
			close;
		}
		mes "Now that the inscriptions are over,";
		mes "what do you choose ?";
		next;
		switch(select("Participate:I've ended my duty:See the rules:Nothing"))
		{
			//==================================================================================================================================
			case 1: //==========================================================================================================================
				query_sql("SELECT tour_actif FROM loup_garou WHERE pseudo='"+strcharinfo(0)+"'",.@Tour_Joueur);
				if(.@Tour_Joueur == 1)
				{
					switch($J_actuel_lg)
					{
						case 1: //Si c'est la voyante
							if($Vue_voyante == 1)
							{
								mes .@npc_spirit$;
								mes "You've already seen this card tonight !";
								close;
							}
							mes .@npc_spirit$;
							mes "Choose the desired nickname..";
							next;
							set $pseudo_lg$,callfunc("Joueur_Vivant");
							set $Vue_voyante, 1;
							mes .@npc_spirit$;
							query_sql("SELECT role2 FROM loup_garou WHERE pseudo ='"+$pseudo_lg$+"'",.@voyante_vue$);
							mes "Thanks. This person is a "+.@voyante_vue$;
							awake "evtlg";
							close;
						case 2: //Si c'est Cupidon
							if($Couple_forme == 1)
							{
								mes .@npc_spirit$;
								mes "You've already made a couple !";
								close;
							}
							mes .@npc_spirit$;
							mes "Choose the desired nickname...";
							next;
							set $pseudo_lg$,"";
							set $pseudo_lg$,callfunc("Joueur_Vivant");
							mes .@npc_spirit$;
							mes "Thanks. Choose the second nickname now..";
							next;
							set $pseudo2_lg$,"";
							set $pseudo2_lg$,callfunc("Joueur_Vivant");
							if($pseudo2_lg$ == $pseudo_lg$)
							{
								mes .@npc_spirit$;
								mes "A player can be married to himself..";
								close;			
							}
							mes .@npc_spirit$;
							mes "Perfect, the couple is made !";
							set $Couple_forme, 1;
							query_sql("UPDATE loup_garou SET conjoint='"+$pseudo_lg$+"' WHERE pseudo='"+$pseudo2_lg$+"'");
							query_sql("UPDATE loup_garou SET conjoint='"+$pseudo2_lg$+"' WHERE pseudo='"+$pseudo_lg$+"'");
							message $pseudo_lg$, "Cupidon decided to bind you to " + $pseudo2_lg$ + " !";
							message $pseudo2_lg$, "Cupidon decided to bind you to " + $pseudo_lg$ + " !";
							awake "evtlg";
							close;
						case 3: //Si c'est Salvateur
							mes .@npc_spirit$;
							mes "Choose the desired nickname...";
							next;
							set $pseudo_lg$,callfunc("Joueur_Vivant");
							query_sql("SELECT pseudo FROM loup_garou WHERE protege=1",.@ancien_protege$);
							if($pseudo_lg$ == .@ancien_protege$)
							{
								mes .@npc_spirit$;
								mes "This player was already chosen last turn...";
								close;
							}
							query_sql("UPDATE loup_garou SET protege=0");
							mes .@npc_spirit$;
							mes "Thanks, this person will be protected !";
							query_sql("UPDATE loup_garou SET protege=1 WHERE pseudo='"+$pseudo_lg$+"'");
							awake "evtlg";
							close;
						case 4: //Si c'est Loup Garou
							mes .@npc_spirit$;
							mes "Choose the desired nickname...";
							next;
							set $pseudo_lg$,callfunc("Joueur_Vivant");
							query_sql("SELECT COUNT(id) as Nb_Lg FROM loup_garou WHERE role2='Loup Garou'", .@nb_lg_temp);
							if(.@nb_lg_temp == 2) //Si il y a 2 loups
							{
								query_sql("UPDATE loup_garou SET vote=1 WHERE pseudo='"+$pseudo_lg$+"'");
								mes .@npc_spirit$;
								mes "Thanks, you're vote was noted !";
								if($indice_victime_lg==2) awake "evtlg";
								close;
							}
							set $victime_lg$, $pseudo_lg$;
							mes .@npc_spirit$;
							mes "Thanks, you're vote was noted !";
							awake "evtlg";
							close;
						case 5: //Si c'est Sorcière
							mes .@npc_spirit$;
							if($victime_lg$ != "") mes $victime_lg$ + " died this night..";
							else mes "Nobody died tonight...";
							mes "What do you wanna do ?";
							next;
							switch(select("Cure him:Kill someone else:Nothing"))
							{
								case 1:
									mes .@npc_spirit$;
									if($potion_vie_lg == 1)
									{
										query_sql("SELECT pseudo FROM loup_garou WHERE protege=1",.@protege$);
										if(.@protege$ == $victime_lg$ || $victime_lg$ == "")
										{
											mes .@npc_spirit$;
											mes "Nobody's died tonight.";
											close;
										}
										mes "Do you wanna cure " + $victime_lg$ + " ?";
										next;
										if(select("Yes:No") == 2)
										{
											mes .@npc_spirit$;
											mes "Ok come back quickly if you change your mind !";
											close;
										}
										query_sql("SELECT role1 FROM loup_garou WHERE pseudo='"+$victime_lg$+"'",.@temp_role1_ancetre$);
										if(.@temp_role1_ancetre$ == "Ancetre")
											set $vie_ancetre, $vie_ancetre + 1;
										mes .@npc_spirit$;
										mes "Allright !";
										charcommand "#effect \"" + $victime_lg$ + "\" 66";
										query_sql("UPDATE loup_garou SET mort=0 WHERE pseudo='"+$victime_lg$+"'");
										set $victime_lg$, "";
										set $potion_vie_lg, 0;
										close;
									}
									mes .@npc_spirit$;
									mes "Sorry, you don't have the life potion anymore.";
									close;
									
								case 2:
									if($potion_mort_lg == 1)
									{
										mes .@npc_spirit$;
										mes "Choose the desired nickname...";
										next;
										set $pseudo_lg$,callfunc("Joueur_Vivant");
										query_sql("SELECT pseudo FROM loup_garou WHERE protege=1",.@protege$);
										if(.@protege$ == $pseudo_lg$)
										{
											mes .@npc_spirit$;
											mes "This person is protected. Choose someone else please !";
											close;
										}
										if($victime_lg$ == $pseudo_lg$)
										{
											mes .@npc_spirit$;
											mes "This person already died because of the Werevolfes.";
											close;
										}	
										mes .@npc_spirit$;
										mes "Perfect !";
										charcommand "#effect \""+$pseudo_lg$+"\" 455";
										query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+$pseudo_lg$+"'");	
										set $victime_sorciere_lg$,"";
										set $victime_sorciere_lg$, $pseudo_lg$;
										set $potion_mort_lg, 0;
										close;
										mes .@npc_spirit$;
										mes "If you change your mind, come back quickly!";
										close;
									}
									mes .@npc_spirit$;
									mes "Sorry but you don't have death potion anymore.";
									close;
								
								case 3:
									mes .@npc_spirit$;
									mes "Come back quickly if you change your mind !";
									close;
							}
						case 6: //Si c'est Voleur
							if($Voleur_vole == 1)
							{
								mes .@npc_spirit$;
								mes "You've already stolen an identity !";
								awake "evtlg";
								close;
							}
							mes .@npc_spirit$;
							mes "Choose the desired nickname...";
							next;
							set $pseudo_lg$,callfunc("Joueur_Vivant");
							set $Voleur_vole,1;
							// Recupere info de l'autre personne
							query_sql("SELECT role1, role2, role3 FROM loup_garou WHERE pseudo='"+$pseudo_lg$+"'", .@role1_new_voleur$, .@role2_new_voleur$, .@role3_new_voleur$);
							// Recupere info ancien voleur
							query_sql("SELECT role1, role2, role3 FROM loup_garou WHERE pseudo='"+strcharinfo(0)+"'", .@role1_last_voleur$, .@role2_last_voleur$, .@role3_last_voleur$);
							//Change info du nouveau voleur
							query_sql("UPDATE loup_garou SET role1 = '"+.@role1_last_voleur$+"', role2 = '"+.@role2_last_voleur$+"', role3 = '"+.@role3_last_voleur$+"' WHERE pseudo='"+$pseudo_lg$+"'");
							//Change info de l'ancien voleur
							query_sql("UPDATE loup_garou SET role1 = '"+.@role1_new_voleur$+"', role2 = '"+.@role2_new_voleur$+"', role3 = '"+.@role3_new_voleur$+"' WHERE pseudo='"+strcharinfo(0)+"'");
							//Fin du changement
							query_sql("UPDATE loup_garou SET tour_actif=0");
							query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Voleur'");
							//Changer les disguise 
							if(.@role1_new_voleur$=="Ancetre") disguise 121;
							else if(.@role1_new_voleur$=="Chasseur") disguise 1403;
							else if(.@role1_new_voleur$=="Sorcière") disguise 1102;
							else if(.@role1_new_voleur$=="Voyante") disguise 803;
							else if(.@role1_new_voleur$=="Cupidon") disguise 1374;
							else if(.@role1_new_voleur$=="Capitaine") disguise 873;
							else if(.@role1_new_voleur$=="Salvateur") disguise 775;
							charcommand "#disguise \""+$pseudo_lg$+"\" 421";
							mes .@npc_spirit$;
							mes "Your identity have been changed. You're now :"+.@role1_new_voleur$+" and "+.@role2_new_voleur$+".";
							if(.@role3_new_voleur$ != "")
								mes "And you're also "+.@role3_new_voleur$+".";
							message $pseudo_lg$,"You're identity have been stolen. You're now "+.@role1_last_voleur$+" and "+.@role2_last_voleur$+" !";
							if(.@role3_last_voleur$ != "")
								message $pseudo_lg$,"And you're also " + .@role3_last_voleur$ + ".";
							//Envoie mp aux autre loup garou si il prend le role du loup garou
							if(.@role2_new_voleur$ == "Loup Garou")
							{
								//Prend le pseudo du 2eme loup garou
								set .@temp_pseudo_perso_lg$, "";
								set .@village_lg$,"";
								query_sql("SELECT pseudo FROM loup_garou",.@village_lg$);
								query_sql("SELECT pseudo FROM loup_garou WHERE role2='Loup Garou' AND pseudo!='"+strcharinfo(0)+"'",.@temp_pseudo_perso_lg$);
								if(.@temp_pseudo_perso_lg$!="")
									message strcharinfo(0),"In the pack, there's "+.@temp_pseudo_perso_lg$+" and you."; // il y a un autre loup garou
								else
									message strcharinfo(0),"You're the only werevolf !"; // il est le seul loup garou
								message .@village_lg$[$role2_temp_2], "In the pack, there's " + .@village_lg$[$role2_temp] + " and you!";
								message .@village_lg$[$role2_temp], "In the pack, there's " + .@village_lg$[$role2_temp_2] + " and you!"; 
							}
							awake "evtlg";
							close;
						case 7: //Si c'est Chasseur
							mes .@npc_spirit$;
							mes "Choose the desired nickname...";
							next;
							set $pseudo_lg$,callfunc("Joueur_Vivant");
							query_sql("SELECT pseudo FROM loup_garou WHERE protege=1",.@protege$);
							if($pseudo_lg$ == .@protege$)
							{
								mes .@npc_spirit$;
								mes "This person is protected. Choose someone else!";
								close;
							}
							mes .@npc_spirit$;
							mes "Perfect !";
							set $pseudo_mort_chasseur$, $pseudo_lg$;
							awake "evtlg";
							close;
						case 8: //Si c'est Capitaine
							set .@temp_joueur_mort, 0;
							mes .@npc_spirit$;
							mes "Choose the desired nickname...";
							next;
							set $pseudo_lg$,callfunc("Joueur_Vivant");
							mes .@npc_spirit$;
							mes "This person is now Captain";
							set $pseudo_new_capitaine$, $pseudo_lg$;
							set $pseudo_ancien_capitaine$,strcharinfo(0);
							awake "evtlg";
							close;
						default:
							mes .@npc_spirit$;
							mes "Humm... I can do nothing for you !";
							close;
					}
				}
				if($Acivation_vote_lg == 1)
				{
					//Test si joueur est en vie
					query_sql("SELECT mort FROM loup_garou WHERE pseudo = '"+strcharinfo(0)+"'",.@mort_joueur_lg);
					if(.@mort_joueur_lg == 1)
					{
						mes .@npc_spirit$;
						mes "Your dead. You can't vote..";
						close;
					}
					//Test si joueur a deja vote
					query_sql("SELECT vote_actif FROM loup_garou WHERE pseudo='"+strcharinfo(0)+"'",.@deja_vote);
					if(.@deja_vote==1)
					{
						mes .@npc_spirit$;
						mes "You've already voted !";
						close;
					}
					mes .@npc_spirit$;
					mes "Choose the desired nickname...";
					next;
					set $vote_hab_actuel$,callfunc("Joueur_Vivant");
					query_sql("SELECT pseudo FROM loup_garou WHERE role1='Capitaine' OR role3='Capitaine'", .@pseudo_cap_temp$);
					if(.@pseudo_cap_temp$ == strcharinfo(0))
						query_sql("UPDATE loup_garou SET vote = vote+2 WHERE pseudo ='"+$vote_hab_actuel$+"'"); // Vote compte double au capitaine
					else
						query_sql("UPDATE loup_garou SET vote = vote+1 WHERE pseudo ='"+$vote_hab_actuel$+"'");
					
					query_sql("UPDATE loup_garou SET vote_actif = 1 WHERE pseudo ='"+strcharinfo(0)+"'");
					mes .@npc_spirit$;
					mes "Thanks, your vote have been noted !";
					close;
				}
				else
				{
					mes .@npc_spirit$;
					mes "Humm... I can do nothing for you !";
					close;
				}
			//================================================================================================================================
			//================================================================================================================================
			case 2:
				awake "evtlg";
				close;
				
			case 3:
				mes .@npc_spirit$;
				mes "Choose your role..";
				next;
				switch(select("Werevolf:Villager:Hunter:Witch:Clairvoyant:Next page"))
				{
					case 1: 
						mes .@npc_spirit$;
						mes "Each night, they rip a villagers throat appart. During the day, the have to hide their identity to stay masked.";
						close; 
						
					case 2:
						mes .@npc_spirit$;
						mes "There's not particular power. They just have to have good instinct.";
						close;
						
					case 3:
						mes .@npc_spirit$;
						mes "The Hunter can kill someone when is designated to die or killed by the wolves.";
						close;
						
					case 4:
						mes .@npc_spirit$;
						mes "She can make two powerful potions. The Life potion can bring the wolves victim to life, and the Death Potion can kill anybody during the night.";
						next;
						mes .@npc_spirit$;
						mes "She can use only one potion by night, and each potion one time only during the whole game.";
						next;
						mes .@npc_spirit$;
						mes "The following day, there can be two, one or no dead at all. The Witch can use her potion for herself to save her life.";
						close;
						
					case 5:
						mes .@npc_spirit$;
						mes "Each night, she can learn a person role. She's here to help the Villagers to find the Wolves.";
						close;
						
					case 6:
						mes .@npc_spirit$;
						mes "Other roles...";
						next;
						switch(select("Cupidon:Captain:Salvator:Thief:Ancestor:I changed my mind"))
						{
							case 1: 
								mes .@npc_spirit$;
								mes "The first night, he'll designated two players, who'll fail in love. None can live without the other. If one die, the other dies by grief.";
								next;
								mes .@npc_spirit$;
								mes "A werewolf and a villager can be lovers. They'll so play both against the rest of the village.";
								next;
								mes .@npc_spirit$;
								mes "If the survive, they won. Cupidon can be designated to be a lover.";
								close; 
								
							case 2:
								mes .@npc_spirit$;
								mes "Captain's voting count twice.";
								close;
								
							case 3:
								mes .@npc_spirit$;
								mes "The Salvator can protect a target from werewolfs rage. He can designated anybody, including himself.";
								next;
								mes .@npc_spirit$;
								mes "On the other hand, he can't designated the same person twice in a row.";
								close;
								
							case 4:
								mes .@npc_spirit$;
								mes "The Thief can trade his role for anybody else. He'll then play this new role.";
								close;
								
							case 5:
								mes .@npc_spirit$;
								mes "The Ancestor is the strongest people in the villager, thanks to the wisdom the years gave him.";
								next;
								mes .@npc_spirit$;
								mes "He won't die if he's designated by the werewolfs. He needs two attacks to die.";
								next;
								mes .@npc_spirit$;
								mes "On the other hand, if he's designated by the Hunter or the Witch, he'll die as anybody else.";
								next;
								mes .@npc_spirit$;
								mes "Other specitificities: if he's cured by the witch, he only regains one life, not both.";
								next;
								mes .@npc_spirit$;
								mes "Once the ancestor dies, everybody in the village lose his power.";
								close;

							case 6:
								mes .@npc_spirit$;
								mes "Doesn't matter. See you soon.";
								close;	
						}
				}
						
			case 4:
				mes .@npc_spirit$;
				mes "No problem !";
				close;
		}
		end;
	}
	mes "Sorry, but the event hasn't start !";
	mes "See you soon..";
	close;
}

//Script de lancement de l'event
-	script	Lancement_ins_LG::evtlg	-1,{

OnStartLg:
	//=============Configuration=================
	set $min_hab,4; //Minimum de joueurs //A changer par 4
	set $max_hab,8; //Maximum de joueurs
	set $event_ins_lg,1; //Lance les inscriptions
	set $vie_ancetre,2; //Donne 2 vies a l'ancetre
	set $joueur_lg,0; //Nombre de joueurs
	set $Couple_forme,0; //Cupidon peut faire que un couple
	set $Acivation_vote_lg,0; //Desactive les votes
	set $potion_vie_lg,1; //Donne une potion de vie a la sorciere
	set $potion_mort_lg,1; //Donne une potion de mort a la sorciere
	query_sql("DELETE FROM `loup_garou`"); //Efface les données de la table
	//Classe disponible role 1
	setarray .@tab_role1_hab$[0],"Voyante", //803
								"Voleur", //421
								"Chasseur", //1403
								"Capitaine", //873
								"Sorcière", //1102
								"Cupidon", //1374 Mec
								"Salvateur", // 775
								"Ancetre"; //121
	//Mort = 801
	//===========================================
	announce "The Thiercellieux Village brought back all its villagers to find the wolves. Be quick, there's only 8 place and 5 minutes to register!",bc_yellow|bc_all;
	sleep 300000; //A changer par 180000
	if($event_lancement_lg == 1 || $joueur_lg >= $min_hab) 
	{
		set .@village$,"";
		query_sql("SELECT pseudo FROM loup_garou",.@village$);
		set $event_lancement_lg, 1;
		set $event_ins_lg, 0;
		mapannounce "thier_vlg","Welcome to all of you to the village council.", bc_map;
		sleep 3500;
		mapannounce "thier_vlg","Tonight, we got :",bc_map;
		sleep 1000;
		for(set .@lg_joueurs, 0 ; .@lg_joueurs < getarraysize(.@village$) ; set .@lg_joueurs, .@lg_joueurs + 1)
		{
			sleep 500;
			mapannounce "thier_vlg",getelementofarray(.@village$,.@lg_joueurs),bc_map;
		}
		sleep 2000;
		mapannounce "thier_vlg","Both of you will receive his power.", bc_map;
		sleep 4000;
		//Mise en place d'un tableau avec choix aleatoire sur les role1
		setarray .@Tab_Temp_Lg$[0], .@tab_role1_hab$[rand($joueur_lg)];
		set .@role$, "";
		for(set $indice1, 1 ; $indice1 < $joueur_lg ; set $indice1, $indice1 + 1)
		{
			set .@role$, .@tab_role1_hab$[rand($joueur_lg)];
			set $lg_role_deja_utilise, 0;
			for(set .@indice2, 0 ; .@indice2 < getarraysize(.@Tab_Temp_Lg$) ; set .@indice2, .@indice2 + 1)
				if(.@role$ == .@Tab_Temp_Lg$[.@indice2])
				{
					set $indice1, $indice1 - 1;
					set $lg_role_deja_utilise, 1;
				}
			if($lg_role_deja_utilise != 1)
				setarray .@Tab_Temp_Lg$[$indice1],.@role$;
		}
		//Mise a jour de la table de chaque utilisateur avec sont role1 + disguise du joueur + message pour savoir son rôle
		set .@village$,"";
		query_sql("SELECT pseudo FROM loup_garou",.@village$);
		for(set .@indice3 ,0 ; .@indice3 < $joueur_lg ; set .@indice3, .@indice3 + 1)
		{
			query_sql("UPDATE `loup_garou` SET role1='"+.@Tab_Temp_Lg$[.@indice3]+"' WHERE pseudo='"+.@village$[.@indice3]+"'");
			message .@village$[.@indice3],"You have a power of "+.@Tab_Temp_Lg$[.@indice3]+".";
			if(.@Tab_Temp_Lg$[.@indice3]=="Ancetre") charcommand "#disguise \""+.@village$[.@indice3]+"\" 121";
			else if(.@Tab_Temp_Lg$[.@indice3]=="Chasseur") charcommand "#disguise \""+.@village$[.@indice3]+"\" 1403";
			else if(.@Tab_Temp_Lg$[.@indice3]=="Sorcière") charcommand "#disguise \""+.@village$[.@indice3]+"\" 1102";
			else if(.@Tab_Temp_Lg$[.@indice3]=="Voyante") charcommand "#disguise \""+.@village$[.@indice3]+"\" 803";
			else if(.@Tab_Temp_Lg$[.@indice3]=="Cupidon") charcommand "#disguise \""+.@village$[.@indice3]+"\" 1374";
			else if(.@Tab_Temp_Lg$[.@indice3]=="Capitaine") charcommand "#disguise \""+.@village$[.@indice3]+"\" 873";
			else if(.@Tab_Temp_Lg$[.@indice3]=="Salvateur") charcommand "#disguise \""+.@village$[.@indice3]+"\" 775";
			else if(.@Tab_Temp_Lg$[.@indice3]=="Voleur") charcommand "#disguise \""+.@village$[.@indice3]+"\" 421";
			sleep 500;
		}
		//Mise en place des roles 2
		set $role2_temp, rand($joueur_lg);
		query_sql("UPDATE `loup_garou` SET role2='Loup Garou' WHERE pseudo='"+.@village$[$role2_temp]+"'");
		message .@village$[$role2_temp], "But you're also a wolf.";
		if($joueur_lg > 6)
		{
			set $role2_temp_2, rand($joueur_lg);
			while ($role2_temp_2 == $role2_temp)
				set $role2_temp_2, rand($joueur_lg);
			query_sql("UPDATE `loup_garou` SET role2='Loup Garou' WHERE pseudo='"+.@village$[$role2_temp_2]+"'");
			message .@village$[$role2_temp_2], "But you're also a wolf.";
		}
		sleep 4000;
		query_sql("SELECT pseudo FROM loup_garou WHERE role2=''",.@Villageois$);
		for(set .@lg_joueurs,0;.@lg_joueurs<getarraysize(.@Villageois$);set .@lg_joueurs,.@lg_joueurs+1)
			message .@Villageois$[.@lg_joueurs],"Mais tu incarnes aussi le rôle de Villageois.";
		query_sql("UPDATE `loup_garou` SET role2='Villageois' WHERE role2=''");
		sleep 4000;
		//Envoie du mp à la meute
		query_sql("SELECT pseudo, COUNT(id) as LoupsGarous FROM loup_garou WHERE role2='Loup Garou'",.@Loups$,.@Nb_Loups);
		if(.@Nb_Loups == 2)
		{
			message .@village$[$role2_temp_2], "There's " + .@village$[$role2_temp] + " other wolves in the group !";
			message .@village$[$role2_temp], "There's " + .@village$[$role2_temp_2] + " other wolves in the group !"; 
		}
		else
			message .@Loups$[0], "You're the only wolf !";
		//Annonces
		sleep 4000;
		mapannounce "thier_vlg", "If you want to know some specificities about a role, ask me.",bc_map;
		sleep 4500;
		mapannounce "thier_vlg", "Knowing this is surely usefull, but who tell you that this person is not also a wolf ???",bc_map;
		sleep 6000;
		mapannounce "thier_vlg", "NOBODY! So be careful !!!",bc_map;
		sleep 3500;
		mapannounce "thier_vlg", "It's your duty to survive, and found the wolves.",bc_map;
		sleep 5000;
		//Lancement
		query_sql ("SELECT COUNT(id) as Vivants FROM loup_garou WHERE mort = 0",.@Nb_Joueurs_Vivants); //Compte le nombre de joueurs vivants
		query_sql("SELECT COUNT(id) as LoupsGarous FROM loup_garou WHERE role2 = 'Loup Garou' AND mort = 0",.@Nb_Role2_LG_Vivants); //Compte le nombre de joueurs en loup garou et vivants
		query_sql("SELECT COUNT(id) as Villageois1 FROM loup_garou WHERE role1 = 'Villageois' AND mort = 0",.@Nb_Role1_Villageois_Vivants); //Compte le nombre de joueurs en villagois role 2et vivants
		query_sql("SELECT COUNT(id) as Villageois2 FROM loup_garou WHERE role2 = 'Villageois' AND mort = 0",.@Nb_Role2_Villageois_Vivants); //Compte le nombre de joueurs en villagois role 2et vivants
		//Tant que 1 loup-garou est vivant OU que 2 joueurs sont vivant OU qu'un villageois est vivant OU partie actif
		while(.@Nb_Role2_LG_Vivants != 0 && .@Nb_Joueurs_Vivants >= 2 && .@Nb_Role2_Villageois_Vivants !=0 && $event_lancement_lg == 1)
		{
			//La nuit tombe..
			mapannounce "thier_vlg","The night fall upon the village..",bc_map,0x737373;
			sleep 4000;
			//Lance un effect les personnes du village
			set .@Village_Vivant$,"";
			query_sql("SELECT pseudo FROM loup_garou WHERE mort=0",.@Village_Vivant$);
			for(set .@lg_joueurs,0;.@lg_joueurs<getarraysize(.@Village_Vivant$);set .@lg_joueurs,.@lg_joueurs+1)
				charcommand "#effect \""+.@Village_Vivant$[.@lg_joueurs]+"\" 197";
			sleep 1000;
			mapannounce "thier_vlg","The village sleeps ... Everybody dreams..",bc_map,0x737373;
			sleep 3000;
			//Lance un effect les personnes du village
			set .@Village_Vivant$,"";
			query_sql("SELECT pseudo FROM loup_garou WHERE mort=0",.@Village_Vivant$);
			for(set .@lg_joueurs,0;.@lg_joueurs<getarraysize(.@Village_Vivant$);set .@lg_joueurs,.@lg_joueurs+1)
				charcommand "#effect \""+.@Village_Vivant$[.@lg_joueurs]+"\" 197";
			sleep 1000;
			//Recherche si une Voyante est vivante
			set $Vue_voyante, 0;
			query_sql("UPDATE loup_garou SET tour_actif=0");
			set .@role_temp_lg,10;
			query_sql("SELECT mort FROM loup_garou WHERE role1 = 'Voyante'", .@role_temp_lg);
			if(.@role_temp_lg == 0)
			{
				atcommand "@npctalk [Spirit] All, except the Clairvoyant, who awake to seek someone's soul.";
				sleep 2500;
				atcommand "@npctalk [Spirit] Clairvoyant, tell me the nickname of the one you wanna analyse.";
				sleep 2000;
				atcommand "@npctalk [Spirit] Be careful, you only have 40 seconds..";
				query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Voyante'");
				set $J_actuel_lg,1;
				sleep 40000;
				atcommand "@npctalk [Spirit] Clairvoyant, you can know fall in sleep again.";
				sleep 2500;
			}
			//Recherche si Cupidon est vivant
			query_sql("UPDATE loup_garou SET tour_actif=0");
			set .@role_temp_lg,10;
			query_sql("SELECT mort FROM loup_garou WHERE role1 = 'Cupidon'", .@role_temp_lg);
			if(.@role_temp_lg==0 && $Couple_forme!=1)
			{
				atcommand "@npctalk [Spirit] Cupidon awakes...";
				sleep 2500;
				atcommand "@npctalk [Spirit] Cupidon will know shoot his arrow to designate the two Lovers.";
				sleep 2000;
				atcommand "@npctalk [Spirit] Cupidon, indicates me the nickname of those you wanna hit.";
				sleep 2000;
				atcommand "@npctalk [Spirit] Be careful, you only have one minute...";
				query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Cupidon'");
				set $J_actuel_lg,2;
				sleep 60000;
				atcommand "@npctalk [Spirit] You can know sleep again, Cupidon,";
				sleep 2500;
				atcommand "@npctalk [Spirit] the Lovers have now received a MP to know their lover.";
				sleep 4000;
			}
			//Recherche si le Salvateur est vivant
			query_sql("UPDATE loup_garou SET tour_actif=0");
			set .@role_temp_lg,10;
			query_sql("SELECT mort FROM loup_garou WHERE role1 = 'Salvateur'", .@role_temp_lg);
			if(.@role_temp_lg == 0)
			{
				atcommand "@npctalk [Spirit] Salvator, my friend! Wake up, and tell me the name of whom you wanna protect,";
				sleep 2500;
				atcommand "@npctalk [Spirit] this people won't be able to die.";
				sleep 2500;
				atcommand "@npctalk [Spirit] Be careful, you only have 40s...";
				query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Salvateur'");
				set $J_actuel_lg,3;
				sleep 40000;
				atcommand "@npctalk [Spirit] Perfect, you can go back to sleep.";
				sleep 4000;
			}
			//Recherche si un loup garou est vivant
			set $victime_lg$, "";
			query_sql("UPDATE loup_garou SET tour_actif=0, vote=0");
			set .@role_temp_lg,10;
			query_sql("SELECT mort FROM loup_garou WHERE role2 = 'Loup Garou'", .@role_temp_lg);
			if(.@role_temp_lg == 0)
			{
				atcommand "@npctalk [Spirit] While everybody's asleep, Wolves start to howl...";
				sleep 2500;
				atcommand "@npctalk [Spirit] They should now MP themselves to discuss and designated someone.";
				sleep 2500;
				atcommand "@npctalk [Spirit] once done, each of them should tell me the name.";
				sleep 2000;
				atcommand "@npctalk [Spirit] Be careful, you only have 40s..";
				query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role2='Loup Garou'");
				set $J_actuel_lg,4;
				set $victime_lg$, "";
				sleep 40000;
				query_sql("SELECT COUNT(id) as Nb_Lg FROM loup_garou WHERE role2='Loup Garou'", .@nb_lg_temp);
				if(.@nb_lg_temp == 2) //Si il y a 2 loups
				{
					query_sql("SELECT COUNT(id) FROM loup_garou WHERE vote=(SELECT MAX(vote) FROM loup_garou)",.@Nb_Votes);
					query_sql("SELECT MAX(vote) FROM loup_garou",.@Max_Votes);
					//================================================================
					if(.@Nb_Votes != 1 || .@Max_Votes == 0)
					{
						atcommand "@npctalk [Spirit] Wolves didn't attack, they can go back to sleep.";
						sleep 2500;
					}
					else
					{
						query_sql("SELECT pseudo FROM loup_garou WHERE vote="+.@Max_Votes,$victime_lg$);
						query_sql("SELECT role1 FROM loup_garou WHERE pseudo='"+$victime_lg$+"'", .@temp_role1_ancetre$);
						if(.@temp_role1_ancetre$ == "Ancetre")
						{
							if($vie_ancetre == 2)
							{
								set $vie_ancetre, $vie_ancetre-1;
								specialeffect 455, $victime_lg$, "Spirit";
								atcommand "@npctalk [Spirit] The wolves bite the Ancestor. Thanks to his wisdom, he's only hurt !";
								sleep 1000;
								atcommand "@npctalk [Spirit] they can now go back to sleep.";
								sleep 2500;
							}
							if($vie_ancetre == 1)
							{
								set $vie_ancetre, $vie_ancetre - 1;
								charcommand "#effect \""+$victime_lg$+"\" 455";
								query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+$victime_lg$+"'");	
								atcommand "@npctalk [Spirit] The wolves attacked. They can now go back to sleep.";
								sleep 2500;
							}
						}
						else
						{
							charcommand "#effect \""+$victime_lg$+"\" 455";
							query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+$victime_lg$+"'");
							atcommand "@npctalk [Spirit] The wolves attacked. They can now go back to sleep.";
							sleep 2500;
						}
					}
				}
				else
				{
					//Si il a tué qq
					if($victime_lg$ != "")
					{
						query_sql("SELECT role1 FROM loup_garou WHERE pseudo='"+$victime_lg$+"'",.@temp_role1_ancetre$);
						if(.@temp_role1_ancetre$ == "Ancetre")
						{
							if($vie_ancetre == 2)
							{
								set $vie_ancetre,$vie_ancetre - 1;
								atcommand "@npctalk [Spirit] The wolf bite the Ancestor. Thanks to his wisdom, he's only hurt !";
								sleep 1000;
								atcommand "@npctalk [Spirit] He can now go back to sleep.";
								sleep 2500;
							}
							if($vie_ancetre == 1)
							{
								set $vie_ancetre, $vie_ancetre - 1;
								charcommand "#effect \""+$victime_lg$+"\" 455";
								query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+$victime_lg$+"'");			
								atcommand "@npctalk [Spirit] The wolf attacked. He can now go back to sleep.";
								sleep 2500;
							}
						}
						else
						{
							charcommand "#effect \""+$victime_lg$+"\" 455";
							query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+$victime_lg$+"'");			
							atcommand "@npctalk [Spirit] The wolf attacked. He can now go back to sleep.";
							sleep 2500;
						}
					}
					else
					{
						atcommand "@npctalk [Spirit] The wolf didn't attack. He can now go back to sleep.";
						sleep 2500;
					}
				}
			}
			//Recherche si la sorcière est vivante
			query_sql("UPDATE loup_garou SET tour_actif=0");
			set .@role_temp_lg,10;
			set $victime_sorciere_lg$, "";
			query_sql("SELECT mort FROM loup_garou WHERE role1 = 'Sorcière'", .@role_temp_lg);
			if(.@role_temp_lg == 0)
			{
				atcommand "@npctalk [Spirit] In the meantime, the Witch awakes.";
				sleep 2500;
				atcommand "@npctalk [Spirit] Witch, if you want to use your power, talk to me.";
				sleep 2000;
				atcommand "@npctalk [Spirit] Be careful, you only have 40s..";
				query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Sorciere'");
				set $J_actuel_lg,5;
				sleep 40000;
				atcommand "@npctalk [Spirit] The Witch goes back to bed.";
				sleep 2500;
			}
			//Recherche si le Voleur est vivant
			query_sql("UPDATE loup_garou SET tour_actif=0");
			set .@role_temp_lg,10;
			set $Voleur_vole, 0;
			query_sql("SELECT mort FROM loup_garou WHERE role1 = 'Voleur'",.@role_temp_lg);
			if(.@role_temp_lg == 0)
			{
				atcommand "@npctalk [Spirit] The Thief stop his alarm clock, and sneak into the village.";
				sleep 2500;
				atcommand "@npctalk [Spirit] Tell me, who do you wanna thief ?";
				sleep 2000;
				atcommand "@npctalk [Spirit] Be careful, you only have 40s..";
				query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Voleur'");
				set $J_actuel_lg,6;
				sleep 40000;
				atcommand "@npctalk [Spirit] Thief, you can go back to sleep.";
				sleep 2500;
			}
			//Le jour se lève..
			query_sql("UPDATE loup_garou SET tour_actif=0");
			set $J_actuel_lg,0;
			mapannounce "thier_vlg", "The day raise...", bc_map,0x737373;
			sleep 3000;
			mapannounce "thier_vlg","Everybody wake up.", bc_map,0x737373;
			sleep 4000;
			//Annonce si quelqu'un est mort
			//Si la sorcière a tuer qu'un
			if($victime_sorciere_lg$ != "")
			{
				atcommand "@npctalk [Spirit] Tonight, the Witch decided to kill "+$victime_sorciere_lg$;
				sleep 2500;
				atcommand "@npctalk [Spirit] grâce à sa potion d'empoisonnement.";
				charcommand "#effect \""+$victime_sorciere_lg$+"\" 124";
				query_sql("SELECT role2 FROM loup_garou WHERE pseudo ='"+$victime_sorciere_lg$+"'",.@temp_lg_role2$);
				callfunc "Declaration_Role",.@temp_lg_role2$,$victime_sorciere_lg$;
				callfunc "Mort",$victime_sorciere_lg$;
				charcommand "#disguise \""+$victime_sorciere_lg$+"\" 801";
				callfunc "Mort_Conjoint",$victime_sorciere_lg$;
				sleep 2000;
			}
			//Si les loup garou ont tué qq
			if($victime_lg$ != "")
			{
				atcommand "@npctalk [Spirit] Tonight "+$victime_lg$+" was devoured by the wolves.";
				set .@temp_lg_role2$,"";
				query_sql("SELECT role2 FROM loup_garou WHERE pseudo ='"+$victime_lg$+"'",.@temp_lg_role2$);
				callfunc "Declaration_Role",.@temp_lg_role2$,$victime_lg$;
				callfunc "Mort",$victime_lg$;
				charcommand "#disguise \""+$victime_lg$+"\" 801";
				callfunc "Mort_Conjoint",$victime_lg$;
				sleep 2000;
			}
			else
			{
				atcommand "@npctalk [Spirit] Wolves wasn't agree. Nobody died..";
				sleep 2000;
			}
			//==Suites texte
			set .@Fin_event,0;
			set .@Fin_event,callfunc("Fin_Event_LG");
			if(.@Fin_event==1) break;
			sleep 2500;
			atcommand "@npctalk [Spirit] If you have some doubt, or something to say.";
			sleep 2500;
			atcommand "@npctalk [Spirit] That's the moment. I'll leave you a minute.";
			sleep 4000;
			atcommand "@npctalk [Spirit] Then, the votes will eliminate someone.";
			query_sql("UPDATE loup_garou SET vote = 0");
			set $Acivation_vote_lg, 1;
			query_sql("UPDATE loup_garou SET vote_actif = 0");
			set $J_actuel_lg,0;
			sleep 60000;
			set $Acivation_vote_lg, 0;
			//Regarde tous les votes
			query_sql("SELECT COUNT(id) FROM loup_garou WHERE vote=(SELECT MAX(vote) FROM loup_garou)",.@Nombre_Perso_Elimine_lg);
			query_sql("SELECT MAX(vote) FROM loup_garou",.@Max_Perso_Elimine_lg);
			//================================================================
			set $victime_village_lg$, "";
			if(.@Nombre_Perso_Elimine_lg != 1 || .@Max_Perso_Elimine_lg == 0)
				atcommand "@npctalk [Spirit] Villagers can't agreed... How bad !";
			else
			{
				//Le village a tué qq
				set $victime_village_lg$,"";
				query_sql("SELECT pseudo FROM loup_garou WHERE vote=(SELECT MAX(vote) FROM loup_garou)", $victime_village_lg$);
				set .@protege$,"";
				query_sql("SELECT pseudo FROM loup_garou WHERE protege=1",.@protege$);
				if(.@protege$ != $victime_village_lg$ && $victime_village_lg$ != "")
				{
					atcommand "@npctalk [Spirit] The village decided to eliminate "+$victime_village_lg$;
					query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+$victime_village_lg$+"'");							
					sleep 2500;
					set .@temp_lg_role2$,"";
					query_sql("SELECT role2 FROM loup_garou WHERE pseudo ='"+$victime_village_lg$+"'",.@temp_lg_role2$);
					callfunc "Declaration_Role",.@temp_lg_role2$,$victime_village_lg$;
					callfunc "Mort",$victime_village_lg$;
					charcommand "#disguise \""+$victime_village_lg$+"\" 801";
					callfunc "Mort_Conjoint",$victime_village_lg$;
				}
				else
				{
					set $victime_village_lg$, "";
					atcommand "@npctalk [Spirit] The village decided to eliminate someone protected by the Salvator.";
					sleep 1500;
					atcommand "@npctalk [Spirit] Therefor, that's not possible.";
				}
				sleep 3000;
				set .@fin_event_lg,0;
				set .@fin_event_lg,callfunc("Fin_Event_LG");
				if(.@fin_event_lg==1) break;
			}
			sleep 3000;
		}
		//Fin
		mapannounce "thier_vlg","The Werewolves event is over. Congratulations !!",bc_map;
		set $event_lancement_lg,0;
		set $event_ins_lg,0;
		set .@village$,"";
		query_sql("SELECT pseudo FROM loup_garou",.@village$);
		for(set .@lg_joueurs,0;.@lg_joueurs<getarraysize(.@village$);set .@lg_joueurs,.@lg_joueurs+1)
		{
			charcommand "#undisguise \""+.@village$[.@lg_joueurs]+"\"";
			charcommand "#warp \""+.@village$[.@lg_joueurs]+"\" thier_vlg 55 55";
		}
		end;
	}
	mapannounce "thier_vlg","3 minutes passed, and not enought inscription.. How bad !",bc_map;
	set $event_lancement_lg,0;
	set $event_ins_lg,0;
	set .@village$,"";
	query_sql("SELECT pseudo FROM loup_garou",.@village$);
	for(set .@lg_joueurs,0;.@lg_joueurs<getarraysize(.@village$);set .@lg_joueurs,.@lg_joueurs+1)
	{
		charcommand "#undisguise \""+.@village$[.@lg_joueurs]+"\"";
		charcommand "#warp \""+.@village$[.@lg_joueurs]+"\" thier_vlg 55 55";
	}
	end;
}

function	script	Mort	{

	//=========================================================Roles
	query_sql("SELECT role1, role3 FROM loup_garou WHERE pseudo = '"+getarg(0)+"'",.@temp_mort_role1$,.@temp_mort_role3$);
	//================================================================
	//Si Chasseur
	query_sql("UPDATE loup_garou SET tour_actif=0");
	if(.@temp_mort_role1$ == "Chasseur")
	{
		callfunc "Mort_Chasseur";
	}
	//Si Ancetre
	query_sql("UPDATE loup_garou SET tour_actif=0");
	if(.@temp_mort_role1$ == "Ancetre")
	{
		callfunc "Mort_Ancetre";
	}
	//Si Capitaine
	query_sql("UPDATE loup_garou SET tour_actif=0");
	set .@Nb_Joueurs_Vivants,10;
	query_sql("SELECT COUNT(id) as Vivants FROM loup_garou WHERE mort = 0",.@Nb_Joueurs_Vivants); //Compte le nombre de joueurs vivants
	//Si il y a encore 2 joueurs en vie minimum
	if(.@Nb_Joueurs_Vivants>=1)
		if(.@temp_mort_role1$=="Capitaine" || .@temp_mort_role3$=="Capitaine")
		{
			callfunc "Mort_Capitaine";
		}
		
	return;
}

function	script	Mort_Conjoint	{

	//=========================================================Conjoint
	query_sql("SELECT conjoint FROM loup_garou WHERE pseudo = '"+getarg(0)+"'",.@mort_conjoint$);
	//================================================================
	//Si la victime a un conjoint
	if(.@mort_conjoint$!="")
	{
		set $mort_role1_conjoint$,"";
		//=========================================================Roles du conjoint
		query_sql("SELECT role1, role2, role3 FROM loup_garou WHERE pseudo = '"+.@mort_conjoint$+"'",$mort_role1_conjoint$,.@mort_role2_conjoint$,$mort_role3_conjoint$);
		//================================================================
		//Test si conjoint est protege !
		query_sql("SELECT protege FROM loup_garou WHERE pseudo='"+.@mort_conjoint$+"'",.@protege_couple);
		if(.@protege_couple==1)
		{
			sleep 2500;
			atcommand "@npctalk [Spirit] And it appears that "+getarg(0)+" was bound to "+.@mort_conjoint$;
			sleep 2500;
			atcommand "@npctalk [Spirit] But as "+.@mort_conjoint$+" was protected, he can't die !";
			sleep 2000;
		}
		else
		{
			//Annonce la mort du conjoint
			sleep 2500;
			atcommand "@npctalk [Spirit] And it appears that "+getarg(0)+" was bound to "+.@mort_conjoint$;
			sleep 2500;
			atcommand "@npctalk [Spirit] Sorry, but "+.@mort_conjoint$+" dies by grief..";
			query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+.@mort_conjoint$+"'");
			callfunc "Declaration_Role",.@mort_role2_conjoint$,.@mort_conjoint$;
			callfunc "Mort",.@mort_conjoint$;
			charcommand "#disguise \""+.@mort_conjoint$+"\" 801";
			sleep 2000;
		}
	}
	
	return;
}

function	script	Mort_Capitaine	{
	
	set $pseudo_new_capitaine$,"";
	set $pseudo_ancien_capitaine$,"";
	atcommand "@npctalk [Spirit] As the designated was the Captain, he leaves his place to someone in the Village,";
	sleep 2000;
	atcommand "@npctalk [Spirit] Captain, who's your successor ?";
	sleep 2000;
	atcommand "@npctalk [Spirit] You have 40s to make a choice !";
	query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Capitaine' OR role3='Capitaine'");
	set $J_actuel_lg,8;
	sleep 40000;
	//Si le capitaine a saisi un pseudo
	if($pseudo_new_capitaine$!="")
	{
		atcommand "@npctalk [Spirit] The Captain leaves his place to "+$pseudo_new_capitaine$;
		query_sql("UPDATE loup_garou SET role3='Capitaine' WHERE pseudo='"+$pseudo_new_capitaine$+"'");
		query_sql("UPDATE loup_garou SET role1='' WHERE pseudo='"+$pseudo_ancien_capitaine$+"'");
		sleep 2500;
	}
	//Si il a rien saisi alors choix aleatoire
	else
	{
		set $num_alea_new_cap,rand($joueur_lg);
		set $temp_cap_trouve,0;
		while($temp_cap_trouve==0)
		{
			set .@temp_cap,10;
			query_sql("SELECT mort FROM loup_garou WHERE pseudo='"+.@village$[$num_alea_new_cap]+"'",.@temp_cap);
			if(.@temp_cap==0)
			{
				set $temp_cap_trouve,1;
			}
			else
			{
				set $num_alea_new_cap,rand($joueur_lg-1);
			}
		}
		if($temp_cap_trouve==1)
		{
			atcommand "@npctalk [Spirit] The new Captain is "+.@village$[$num_alea_new_cap];
			query_sql("UPDATE loup_garou SET role3='Capitaine' WHERE pseudo='"+.@village$[$num_alea_new_cap]+"'");
			sleep 2500;
		}
	}
	set $J_actuel_lg,0;
	
	return;				
}

function	script	Mort_Ancetre	{

	atcommand "@npctalk [Spirit] What a shame...The Ancestor died..";
	sleep 2000;
	atcommand "@npctalk [Spirit] The village losing his wisdom, everybody become a simple villager again.";
	sleep 2000;
	atcommand "@npctalk [Spirit] Unfortunatelly, the wolves are still there..";
	sleep 2000;
	//Transformation des disguises et des roles
	query_sql("UPDATE `loup_garou` SET role1='Villageois',role3=''");
	query_sql("SELECT pseudo FROM loup_garou WHERE mort=0",.@village_vivant_lg$);
	for(set .@indice3, 0 ; .@indice3 < getarraysize(.@village_vivant_lg$) ; set .@indice3, .@indice3 + 1)
	{
		message .@village_vivant_lg$[.@indice3],"Tu incarnes le rôle de Villageois.";
		charcommand "#disguise \""+.@village_vivant_lg$[.@indice3]+"\" 97";
		sleep 500;
	}
	sleep 1500;
	return;
	
}

function	script	Mort_Chasseur	{

	atcommand "@npctalk [Spirit] As the victim was a Hunter, he can shoot one last time.";
	sleep 2000;
	atcommand "@npctalk [Spirit] Hunter, who do you wanna shoot ?";
	sleep 2000;
	atcommand "@npctalk [Spirit] You have 40s !";
	query_sql("UPDATE loup_garou SET tour_actif=1 WHERE role1='Chasseur'");
	set $J_actuel_lg,7;
	set $pseudo_mort_chasseur$, "";
	sleep 40000;
	if($pseudo_mort_chasseur$ != "")
	{
		atcommand "@npctalk [Spirit] The Hunter decided to eliminate "+$pseudo_mort_chasseur$;
		sleep 2500;
		query_sql("UPDATE loup_garou SET mort=1 WHERE pseudo='"+$pseudo_mort_chasseur$+"'");
		query_sql("SELECT role2 FROM loup_garou WHERE pseudo ='"+$pseudo_mort_chasseur$+"'",.@temp_lg_role2$);
		callfunc "Declaration_Role",.@temp_lg_role2$,$pseudo_mort_chasseur$;
		callfunc "Mort",$pseudo_mort_chasseur$;
		charcommand "#disguise \""+$pseudo_mort_chasseur$+"\" 801";
		callfunc "Mort_Conjoint",$pseudo_mort_chasseur$;
		set $pseudo_mort_chasseur$, "";
		sleep 2000;
	}
	return;
}

function	script	Fin_Event_LG	{

	query_sql("SELECT COUNT(id) as Vivants FROM loup_garou WHERE mort = 0",.@Nb_Joueurs_Vivants); //Compte le nombre de joueurs vivants
	query_sql("SELECT COUNT(id) as LoupsGarous FROM loup_garou WHERE role2 = 'Loup Garou' AND mort = 0",.@Nb_Role2_LG_Vivants); //Compte le nombre de joueurs en loup garou et vivants
	query_sql("SELECT COUNT(id) as Villageois1 FROM loup_garou WHERE role1 = 'Villageois' AND mort = 0",.@Nb_Role1_Villageois_Vivants); //Compte le nombre de joueurs en villagois role1 et vivants
	query_sql("SELECT COUNT(id) as Villageois2 FROM loup_garou WHERE role2 = 'Villageois' AND mort = 0",.@Nb_Role2_Villageois_Vivants); //Compte le nombre de joueurs en villagois role2 et vivants
	//Si il reste 1 villageois et 1 loup garou
	if(.@Nb_Role2_Villageois_Vivants==1 && .@Nb_Role2_LG_Vivants==1)
	{
		//Recupere le pseudo des derniers joueurs
		query_sql("SELECT pseudo FROM loup_garou WHERE mort=0 AND role2='Villageois'",.@temp_dernier_joueur_villageois$);
		query_sql("SELECT pseudo FROM loup_garou WHERE mort=0 AND role2='Loup Garou'",.@temp_dernier_joueur_lg$);
		//Recherche si ils sont fiancés
		query_sql("SELECT conjoint FROM loup_garou WHERE pseudo ='"+.@temp_dernier_joueur_villageois$+"'",.@temp_dernier_conjoint_villageois$);
		query_sql("SELECT conjoint FROM loup_garou WHERE pseudo ='"+.@temp_dernier_joueur_lg$+"'",.@temp_dernier_conjoint_lg$);
		if(.@temp_dernier_conjoint_lg$==.@temp_dernier_joueur_villageois$)
		{
			atcommand "@npctalk [Spirit] There's only 2 players left and they are..";
			sleep 2000;
			atcommand "@npctalk [Spirit] LOVERS !!";
			sleep 2000;
			atcommand "@npctalk [Spirit] And as one of them is a villager and the other a wolf..";
			sleep 2000;
			atcommand "@npctalk [Spirit] They both won !!!";
			set $event_lancement_lg,0;
			sleep 2000;
			//Donne l'objet au gagnants...
			if (.rewardMode == 0)
			{
				charcommand "#item \""+.@temp_dernier_joueur_villageois$+"\" 675, 1 "; //Bloody Branch [12103]
			}
			else
			{
				//
			}
			return 1;
		}
	}
	//Si il reste plus d'un loup garou et 0 villageois
	else if(.@Nb_Role2_LG_Vivants>=1 && .@Nb_Role2_Villageois_Vivants==0)
	{
		atcommand "@npctalk [Spirit] Congratulation to the wolves who won this game !";
		set $event_lancement_lg,0;
		sleep 2000;
		//Donne l'objet au gagnants...
		query_sql("SELECT pseudo FROM loup_garou",.@village$);
		for(set .@lg_joueurs,0;.@lg_joueurs<getarraysize(.@village$);set .@lg_joueurs,.@lg_joueurs+1)
		{
			set .@choix_mort,1;
			query_sql("SELECT mort FROM loup_garou WHERE pseudo ='"+.@village$[.@lg_joueurs]+"'",.@choix_mort);
			if(.@choix_mort==0)
			{
				//Si il est vivant on donne l'objet
				charcommand "#item \""+.@village$[.@lg_joueurs]+"\" 675, 1 "; //Silver Coin
			}
		}
		return 1;
	}
	//Si il reste plus d'un villageois et 0 loups garou
	else if(.@Nb_Role2_Villageois_Vivants>=1 && .@Nb_Role2_LG_Vivants==0)
	{
		atcommand "@npctalk [Spirit] Congratulations to the villagers who won this game !";
		set $event_lancement_lg,0;
		sleep 2000;
		//Donne l'objet au gagnants...
		query_sql("SELECT pseudo FROM loup_garou",.@village$);
		for(set .@lg_joueurs,0;.@lg_joueurs<getarraysize(.@village$);set .@lg_joueurs,.@lg_joueurs+1)
		{
			set .@choix_mort,1;
			query_sql("SELECT mort FROM loup_garou WHERE pseudo ='"+.@village$[.@lg_joueurs]+"'",.@choix_mort);
			if(.@choix_mort==0)
			{
				//Si il est vivant on donne l'objet
				if (.rewardMode == 1)
				{
					charcommand "#item \""+.@village$[.@lg_joueurs]+"\" 675, 1 "; //Silver Coin
				}
				else
				{
					logmes "[LoupGarou] Le joueur " + strcharinfo(0) + " remporte l'event et " + .rewardAmt + " Currency Points.";
					callfunc ("AddPoints",getcharid(3),.rewardAmt);
				}
			}
		}
		return 1;
	}
	else return 0;

OnInit:
	set .rewardMode, 0;
	set .rewardAmt, 100;
	end;
}

function	script	Declaration_Role	{

	if(getarg(0) == "Loup Garou")
	{
		sleep 2500;
		charcommand "#disguise \""+getarg(1)+"\" 1785";
		atcommand "@npctalk [Spirit] "+getarg(1)+" was a werewolf !";
		sleep 2000;
	}
	else
	{
		sleep 2500;
		charcommand "#disguise \""+getarg(1)+"\" 97";
		atcommand "@npctalk [Spirit] "+getarg(1)+" actually was a villager...";
		sleep 2000;
	}
	return;
}

function	script	Joueur_Vivant	{

	//Presente les pseudos de la partie !
	query_sql("SELECT pseudo FROM loup_garou WHERE mort=0",.@Village_Vivant$);
	set $menu$,"";
	for(set .@k, 0 ; .@k < getarraysize(.@Village_Vivant$); set .@k, .@k + 1 )
	{
		if(.@k < getarraysize(.@Village_Vivant$) - 1)
			set $menu$, $menu$ + .@Village_Vivant$[.@k] + ":";
		else
			set $menu$, $menu$ + .@Village_Vivant$[.@k];
	}
	//Stock le choix
	set .@menu, select($menu$);
	return .@Village_Vivant$[.@menu-1];
}