//===== Alayne Scripts =======================================
//= Sarah And Fenrir Instance
//===== By: ================================================== 
//= Alayne
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//= hercules SVN
//===== Description: ========================================= 
//= The official instance Sarah And Fenrir transcription
//===== Todo Remaining: ======================================
//
//===== Additional Comments: =================================
//
//
//============================================================

dali02,92,145,5	script	Professor Bernhard::alasafpb	4_M_DOCTOR,{
OnStart:
	mes "[Professor Bernhard]";
	mes "Ah! You're finally here!";

	if(SARAFENRIR == 1)
	{
		mes "Ohohoh I can see you've succeeded in this difficult task.";
		mes "Here, you deserve a reward. Which one do you want?";
		if(select(getitemname(28310), getitemname(28311)) == 1)
		{
			next;
			mes "[Professor Bernhard]";
			mes "Here you are. Take great care of it, it's an old memory of Sara...";
			getitem 28310, 1;
			set SARAFENRIR, 2;
			close;
		}
		else
		{
			next;
			mes "[Professor Bernhard]";
			mes "Here you are. Take great care of it, it's an old memory of Sara...";
			getitem 28311, 1;
			set SARAFENRIR, 2;
			close;
		}
	}

	mes "Here, I'll be able to send you to the fight that started everything...";
	mes "Are you ready for that?";

	set .@party_id, getcharid(1);
	set .@saf_leader, ( getcharid(0) == getpartyleader(.@party_id,2) );

	set .@has_instance, SAF_INSTANCE_ID > 0;
	
	set .@instance_delay, ( instance_delay_sarahaf - gettimetick(2) );

	if( .@instance_delay > 0 )
	{
		mes "You have to wait : ^FF0000"+.@instance_delay+" Seconds.^000000";
		close;
	}

	next;
	switch( select( 
		(( .@party_id && .@saf_leader && !.@has_instance )?"Create " + .instance_name$:""),
		(( .@party_id && .@saf_leader && .@has_instance )?"Destroy " + .instance_name$:""),
	"Nevermind."
		)){
	case 1:
		getpartymember .@party_id,2;
		if( .min_party_member >= 1 ){
			set .@origin, getcharid(3);
			set .@gettimetick, gettimetick(2);
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set .@member_count, .@member_count + 1;
					if( BaseLevel < .base_level[0] || BaseLevel > .base_level[1] ) set .@fail, .@fail|1;
					if( Zeny < .instance_zeny ) set .@fail, .@fail|2;
					if( instance_delay_sarahaf > .@gettimetick ) set .@fail, .@fail|4;
					if( .@fail ){
						set .@name$, strcharinfo(0);
						break;
					}
				}
			attachrid( .@origin );
		}
		
		//safean any previously remaining instance
		if(SAF_INSTANCE_ID > 0)
		{
			getpartymember getcharid(1),2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) && SAF_INSTANCE_ID > 0){
					set SAF_INSTANCE_ID, -1;
				}
			instance_destroy instance_id();
		}
		
		if( .@name$ != "" && .@fail ){
			mes "^FF0000[ Failed ]^000000";
			mes "^0055FF"+.@name$+"^000000, You cannot join right now.";
			mes " ";
			mes "^FF0000[ Reason: ]^000000";
			if( .@fail & 1 ) mes "^777777You must be Level: "+.base_level[0]+" - "+.base_level[1]+"^000000";
			if( .@fail & 2 ) mes "^777777You need "+ .instance_zeny +" Zeny^000000";
			if( .@fail & 4 ) mes "^777777You still have cooldown.^000000";
			break;
		}
		
		getpartymember getcharid(1),0;
		if (instance_check_party(.@party_id, .min_party_member, .base_level[0], .base_level[1]) == 0) {
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "Make or join a party with at least "+.min_party_member+" member and try again, with level between " +.base_level[0] + " and " + .base_level[1] + ".";
		}
		else{	
			//RATHENA
			//set .@instance, instance_create( .instance_name$ );
			//HERCULES
			set .@instance, instance_create( .instance_name$, .@party_id, IOT_PARTY );
			
			if( .@instance < 0 ) {
				npctalk .instance_name$ + " reservation failed.";
				mes "^0000FF"+ .instance_name$ +" ^000000 - Reservation Failed!";
				close;
			}
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "^0000FF"+.instance_name$+" ^000000 - Reserved";
			set .@gettimetick, gettimetick(2);
			getpartymember .@party_id,2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set instance_delay_sarahaf, ( .@gettimetick + ( .instance_delay ));
					set Zeny, Zeny - .instance_zeny;
					set SAF_INSTANCE_ID, .@instance;
					dispbottom "["+.instance_name$+"] -"+ .instance_zeny +" Zeny and Instance Cooldown Applied.";
				}
			attachrid( .@origin );
			npctalk .instance_name$ + " has been generated for Party - " + getpartyname( .@party_id );
			
			//HERCULES
			for( set .@i, 0; .@i < getarraysize(.map$); set .@i,.@i+1 )
			{
				if( instance_attachmap( .map$[.@i],.@instance ) == "" ){
					setmapflag .map$[.@i],mf_zone,"Memorial Dungeon";
					npctalk .instance_name$ + " reservation failed due to map attach failure.";
					mes "- ^FF0000Reservation Failed!^000000";
					instance_destroy( .@instance );
					close;
				}
			}
			instance_set_timeout .instance_delay,300,.@instance;
			instance_init(.@instance);
			
			//disable unused npcs for now
			disablenpc instance_npcname( "safwarp#0", instance_id() );
			disablenpc instance_npcname( "safwarp#02", instance_id() );
			disablenpc instance_npcname( "safwarp#1", instance_id() );
			disablenpc instance_npcname( "safwarp#2", instance_id() );
			disablenpc instance_npcname( "safwarp#3", instance_id() );
			disablenpc instance_npcname( "safwarp#4", instance_id() );
			disablenpc instance_npcname( "safwarp#5", instance_id() );
 		}
		break;
	case 2:
		mes .instance_name$ + " Destroyed.";
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && SAF_INSTANCE_ID > 0){
				set SAF_INSTANCE_ID, -1;
			}
		instance_destroy instance_id();
		break;
	default: 
		break;
}
close;

OnPCLoginEvent:
OnPCLogoutEvent:
	set SAF_INSTANCE_ID, -1;
	end;

OnInit:
	setarray .base_level[0],145,175;
	setarray .map$[0],"1@glast";
	set .map_size, getarraysize( .map$ );
	set .min_party_member, 1;
	set .instance_zeny, 0;
	set .instance_delay, 7200;
	set $saf_timer_delay, 3600; //1 hour to clean the instance
	set .instance_name$, "Sarah And Fenrir";
	set .questId, 61300;
	end;
}

dali02,97,145,3	script	Dimensional Device::alasafdd	PORTAL,{
	if( SAF_INSTANCE_ID <= 0 ){
		mes .instance_name$ + " doesn't exist for Party - "+getpartyname( .@party_id );
		close;
	}
	else{
		//RATHENA
		getmapxy(.@map$, .@x, .@y, UNITTYPE_PC);
		//save .@map$, .@x, .@y;
		//switch(instance_enter(.instance_name$)) {
		//	default:
		//		mes "An unknown error has occurred.";
		//		close;
		//	case 2:
		//		mes "The memorial dungeon " + .instance_name$ + " does not exist.";
		//		mes "The party leader did not generate the dungeon yet.";
		//		close;
		//	case 1:
		//		mes "You can enter the dungeon after making the party.";
		//		close;
		//	case 0:
		//		instance_announce instance_id(), strcharinfo(0)+" from party, " +getpartyname( .@party_id )+", is entering the dungeon, "+.instance_name$+".",bc_map,"0x00ff99",FW_NORMAL,12;
		//		end;
		//}
		
		//HERCULES
		savepoint .@map$, .@x, .@y;
		if( has_instance( .map$[0] ) == "" ){
			mes .instance_name$ + " doesnt exist for Party - "+getpartyname( .@party_id );
		}
		else{
			warp .map$[0],373,305;
			instance_announce instance_id(), getpartyname(getcharid(1))+"  entered in the "+.instance_name$+"!",bc_map,"0x00ff99";
		}
	}
	end;

OnInit:
	set .instance_name$, "Sarah And Fenrir";
	setarray .map$[0],"1@glast";
	end;
}

1@glast,101,107,4	script	sarah_and_Fenrir	HIDDEN_WARP_NPC,2,2,{
function summon_normal;
function summon_guardian;
end;


OnInstanceInit:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
	
	set 'instance_saf_level, 0;
	set 'lastLevelStart, 0;
	
	set 'timerCount, 0;
	set 'lastTimer, -1;
	set 'safAnnounceDelay, -1;
	set 'lastSkillCast, -1;
	set 'safLastSkillCast, 1; //number of minute before Fenrir cast last skill, killing Sarah
	set 'safWaitDelay, 5; //number of minute before Fenrir leave after Sarah
	set 'safExitDelay, 3; //number of minute before castle collapse
	initnpctimer;
	end;
	
function	summon_normal	{
	set .@map$, getarg(0);
	set .@label$, instance_npcname( strnpcinfo(0), instance_id() ) + "::OnNormalDied";
	set .@increaseFloor, getarg(2);

	if(.@increaseFloor == 1)
	{
		'instance_saf_level++;
	}
	
	switch( 'instance_saf_level ){
		case 1:
			//red gallion
			setarray .@monster,3198;
			setarray .@amount,5;
			setarray .@coordX[0], 348, 369; //2 x for each mob to define an area
			setarray .@coordY[0], 303, 288; //2 x for each mob to define an area
			break;
		case 2:
			//Chimera
			setarray .@monster,3197,3199,3200;
			setarray .@amount,30,10,40;
			setarray .@coordinate[0], 328, 52, 60, 278;
			break;
		case 3:
			//Ancient Gigantes, Gigantes
			setarray .@monster,3193,3196,3193,3193,3196,3193;
			setarray .@amount,1,1,1,1,1,1;
			setarray .@coordX[0], 110, 110, 100, 100, 109, 109, 299, 299, 289, 289, 287, 287; //2 x for each mob to define an area
			setarray .@coordY[0], 146, 146, 248, 248, 343, 343, 248, 248, 344, 344, 146, 146; //2 x for each mob to define an area
			break;
		case 4:
			//sarah summons
			if(rand(0,1) == 1)
			{
				setarray .@monster,3191;
			}
			else
			{
				setarray .@monster,3192;
			}
			setarray .@amount,1;
			//random pos pop
			switch(rand(0,3))
			{
				case 0:
					setarray .@coordinate,187,340,191,345;
					break;
				case 1:
					setarray .@coordinate,212,340,208,344;
					break;
				case 2:
					setarray .@coordinate,182,333,184,327;
					break;
				case 3:
					setarray .@coordinate,217,332,216,328;
					break;
			}
			set .@label$, instance_npcname( strnpcinfo(0), instance_id() ) + "::OnSummonDied";
			break;
		case 5:
			//exit monsters Ancient Gigantes, Gigantes, Chimera, Gargoyle, Dragon Mutant 
			setarray .@monster,3191, 3192, 3193, 3194, 3195, 3196, 3200, 3197, 3199;
			setarray .@amount,5, 5, 5, 5, 5, 5, 30, 40, 20;
			setarray .@coordinates[0], 328, 52, 60, 278;
			break;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		set .@target_amount, ( .@amount[.@i] );
		if(getarraysize(.@coordX) > 0)
		{
			set .@coordinate[0], .@coordX[.@i * 2];
			set .@coordinate[1], .@coordY[.@i * 2];
			set .@coordinate[2], .@coordX[.@i * 2 + 1];
			set .@coordinate[3], .@coordY[.@i * 2 + 1];
		}
		areamonster .@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount, .@label$;
	}
	return;
}

function	summon_guardian	{
	set .@map$, getarg(0);
	set .@npcname$, instance_npcname( strnpcinfo(0), instance_id() );
	
	setarray .@coordinate,200,334,200,334;
	
	'instance_saf_level++;
			
	switch( 'instance_saf_level ){
		case 4:
			//Sarah Irene MVP
			setarray .@monster,3190;
			break;
	}
	set .@monster_size, getarraysize( .@monster );
	for(set .@i,0; .@i<.@monster_size; set .@i,.@i+1)
	{
		set .@target_amount, 1;
		set .@size, 0;
		set .@label$, .@npcname$ + "::OnGuardianDied";
		//RATHENA
		//areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
		//set 'safBossId, $@mobId[0];
		//HERCULES
		set 'safBossId, monster(.@map$,.@coordinate[0],.@coordinate[1],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
		set 'safBossAlive, 1;
	}
	setmapflag .@map$,mf_monster_noteleport;
	summon_normal( .@map$, .@npcname$, 0 );
	return;
}

OnSummonDied:
OnNormalDied:
	//nothing used to clean
	end;
	
OnGuardianDied:
  	set 'safBossAlive, 0;
	set .@map$, strnpcinfo(4);	
	instance_announce instance_id(), "[Sarah] Urgh...ARGHHHHH!!!!!!", bc_map, 0x0000FF;
		
	disablenpc instance_npcname( "alasaff4", instance_id() );
	disablenpc instance_npcname( "alasafs", instance_id() );
		
	enablenpc instance_npcname( "alasaff5", instance_id() );
	enablenpc instance_npcname( "alasafs2", instance_id() );

	killmonster strnpcinfo(4), instance_npcname( strnpcinfo(0), instance_id() ) + "::OnSummonDied";

	goto OnAfterLastFight;
	end;
	
OnTimer10000:
	//each 10s, increase timer
	set 'timerCount, 'timerCount + 10;
	if('timerCount == $saf_timer_delay)
	{
		instance_announce instance_id(), "[ Sarah And Fenrir ] The Dimension collapse! I have to get you out of there!", bc_map, 0xFF0000;
		sleep 5000;
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && SAF_INSTANCE_ID > 0){
				set SAF_INSTANCE_ID, -1;
			}
		instance_destroy instance_id();
		stopnpctimer;
	}
	if('safAnnounceDelay >= 0)
	{
		set 'safAnnounceDelay, 'safAnnounceDelay + 10;
		if('safAnnounceDelay == 60 || 'safAnnounceDelay == 120 || 'safAnnounceDelay == 180
			|| 'safAnnounceDelay == 240 || 'safAnnounceDelay == 300)
		{
			if('safAnnounceDelay == 'safWaitDelay * 60)
			{
				instance_announce instance_id(), "[Fenrir] I have to continue...Take the warp to get out. Thanks for your help!", bc_map, 0x0000FF;
				set 'safReadyToWarp, 0;
				set 'safAnnounceDelay, -1;
				disablenpc instance_npcname( "alasaff2", instance_id() );
				disablenpc instance_npcname( "alasaff2_2", instance_id() );
			}
			else if('safAnnounceDelay < 'safWaitDelay * 60)
			{
				instance_announce instance_id(), "[Fenrir] Hurry up! I'll let you " + ('safWaitDelay - ('safAnnounceDelay / 60)) + " minutes before leaving you there!", bc_map, 0x0000FF;				
			}
		}
	}
	if('lastSkillCast >= 0)
	{
		set 'lastSkillCast, 'lastSkillCast + 10;
		if('lastSkillCast == 'safLastSkillCast * 60 && 'safBossAlive == 1)
		{
			doevent instance_npcname( "alasaff4", instance_id() ) + "::OnLastSkillCast";
		} 
	}
	if('lastTimer >= 0)
	{
		set 'lastTimer, 'lastTimer + 10;
		if('lastTimer == 60 || 'lastTimer == 120 || 'lastTimer == 180
			|| 'lastTimer == 240 || 'lastTimer == 300)
		{
			if('lastTimer == 'safExitDelay * 60)
			{
				instance_announce instance_id(), "[Fenrir] You're too slow. I'll cast a general teleportation, get ready", bc_map, 0x0000FF;
				sleep2 1500;
				getpartymember getcharid(1),2;
				for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
					if( attachrid( $@partymemberaid[.@i] ) && SAF_INSTANCE_ID > 0){
						set SAF_INSTANCE_ID, -1;
					}
				instance_destroy instance_id();
			}
			else
			{
				instance_announce instance_id(), "[Fenrir] Hurry up! I'll let you " + ('safExitDelay - ('lastTimer / 60)) + " minutes before leaving you there!", bc_map, 0x0000FF;				
			}
		}
	}
	if('instance_saf_level == 4 && 'lastTimer == -1 && 'lastSkillCast == -1)
	{
		set .@map$, strnpcinfo(4);
		set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
			
		//another pop each 10s
		summon_normal( .@map$, .@npcname$, 0 );
	}
	setnpctimer 0;
	end;
	
OnFirstFloorOn:
	set .@map$, strnpcinfo(4);
	summon_normal( .@map$, .@npc_name$, 1 );
	end;
	
OnSecondFloorOn:
	set .@map$, strnpcinfo(4);
	summon_normal( .@map$, .@npc_name$, 1 );
	end;
	
OnThirdFloorOn:
	set .@map$, strnpcinfo(4);
	summon_normal( .@map$, .@npc_name$, 1 );
	end;	
	
OnLastFightStart:
	set .@map$, strnpcinfo(4);
	set 'lastLevelStart, 0;
	summon_guardian( .@map$, .@npc_name$ );
	end;
	
OnAfterLastFight:
	set .@map$, strnpcinfo(4);
	summon_normal( .@map$, .@npc_name$, 1 );
	end;	
}

1@glast,359,294,5	script	Fenrir::alasaff	4_F_FENRIR,5,5,{
	end;
	
OnTouch:
	if('talking == 0)
	{
		set 'safTalking, getcharid(3);
		set 'talking, 1;
		cutin "fenrir_a", 2;
		instance_announce instance_id(), "[Fenrir] That's strange...I don't recall Glast Heim castle to be filled with such a dark presence...", bc_map, 0x0000FF;
		sleep2 1500;
		charcommand "#me " + strcharinfo(0) + " Hu?! There's someone here? Hello, you must be a traveller!";
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Greetings can come later. Careful! It seems somebody has summoned a monster!", bc_map, 0x0000FF;
		
		cutin "", 255;
		doevent instance_npcname( "sarah_and_Fenrir", instance_id() ) + "::OnFirstFloorOn";
		
		charcommand "#me " + strcharinfo(0) + " What are those things?!";
		sleep2 1500;
		//progressbar "008800",9;
		instance_announce instance_id(), "[Fenrir] Buy me some times while I cast some magic! I'll handle this!", bc_map, 0x0000FF;
		sleep2 3000;
		instance_announce instance_id(), "[Fenrir] Wrath of the True Sky", bc_map, 0x0000FF;
		sleep2 3000;
		instance_announce instance_id(), "[Fenrir] Thunderbolt that may split the Earth!", bc_map, 0x0000FF;
		sleep2 3000;
		instance_announce instance_id(), "[Fenrir] Fall before me right now!", bc_map, 0x0000FF;
		//skill Thunderbolt, kill monsters
		specialeffect 30;
		killmonster strnpcinfo(4), "All";

		sleep2 1500;
		cutin "fenrir_a", 2;
		instance_announce instance_id(), "[Fenrir] Glastheim...Once was the cathedral for Asgard Gods!", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Just it's just another monster lair...", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Wait, thinking of it, something's not right.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Gallions aren't the monsters living in this place...", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] By the way, I know it was sudden but thanks for helping me.", bc_map, 0x0000FF;
		sleep2 1500;
		npctalk "And...Who are you?";
		instance_announce instance_id(), "[Fenrir] And...Who are you?", bc_map, 0x0000FF;
		sleep2 1500;
		charcommand "#me " + strcharinfo(0) + " Ah...That is...";
		sleep2 1500;
		charcommand "#me " + strcharinfo(0) + " I'm just a traveller, wandering around the world.";
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Oh. Is that so? I see. I guess it does not really matters anyway.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] This place isn't safe. It's not to late to turn back.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] There's something I must do. I need to hurry now. Thank you for your help.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Maybe you can help me...", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] No I can't. Anyway, I have to go. Thanks again, maybe we'll see each other later.", bc_map, 0x0000FF;
			
		disablenpc strnpcinfo(3);
				
		cutin "", 255;
		sleep2 1500;
		charcommand "#me " + strcharinfo(0) + " Did that person really was...The Fenrir? No, no, probably not.";
		
		viewpoint 1,212,47,1,0xFF0000;

		enablenpc instance_npcname( "alasaff2", instance_id() );
		enablenpc instance_npcname( "safwarp#0", instance_id() );
		enablenpc instance_npcname( "safwarp#02", instance_id() );
		doevent instance_npcname( "sarah_and_Fenrir", instance_id() ) + "::OnSecondFloorOn";
	}
	end;
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	end;
}


1@glast,212,47,5	script	Fenrir::alasaff2	4_F_FENRIR,5,5,{
	end;
	
OnTouch:
	if('talking == 0)
	{
		set 'safTalking, getcharid(3);
		set 'talking, 1;
		cutin "fenrir_a", 2;
		instance_announce instance_id(), "[Fenrir] You're still in Glast Heim? Why don't you leave this dangerous place?", bc_map, 0x0000FF;
		sleep2 1500;
		cutin "", 255;
		charcommand "#me " + strcharinfo(0) + " I know it could be dangerous, but I thought it can be fun. So I couldn't help myself!";
		sleep2 1500;
		cutin "fenrir_a", 2;
		instance_announce instance_id(), "[Fenrir] Hum adventurers...", bc_map, 0x0000FF;
		sleep2 1500;
		cutin "", 255;
		charcommand "#me " + strcharinfo(0) + " You don't have to worry for me. I'm quite strong actually!";
		sleep2 1500;
		cutin "fenrir_a", 2;
		instance_announce instance_id(), "[Fenrir] Oh really? Maybe you can help me then?", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] This...Thing I'm looking for is here, somewhere in Glastheim.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] But others will come soon to find it before me.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] We don't have much time before they arrive. 5 minutes will be a max.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] I'll go ahead, and I'll wait you before continuing. Remember, 5 minutes.",bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Past this delay, I'll considere you decided to stay aback. And I'll go on by myself.", bc_map, 0x0000FF;
		
		cutin "", 255;
		
		viewpoint 0,212,47,1,0xFF0000;
		viewpoint 1,199,271,1,0x00FF00;
		
		viewpoint 1,110,146,2,0xF9FB00;
		viewpoint 1,100,248,3,0xF9FB00;
		viewpoint 1,109,343,4,0xF9FB00;
		viewpoint 1,299,248,5,0xF9FB00;
		viewpoint 1,289,344,5,0xF9FB00;
		viewpoint 1,287,146,7,0xF9FB00;
		
		doevent instance_npcname( "sarah_and_Fenrir", instance_id() ) + "::OnThirdFloorOn";
		set 'safAnnounceDelay, 0;

		disablenpc strnpcinfo(3);
		enablenpc instance_npcname( "alasaff2_2", instance_id() );		

		set 'safReadyToWarp, 1;
	}
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	set 'talking, 0;
	set 'safReadyToWarp, 0;
	end;
}

1@glast,200,226,5	script	Fenrir::alasaff2_2	4_F_FENRIR,5,5,{
	end;

OnTouch:
	if('safReadyToWarp == 1 && 'talking == 0)
	{
		set 'safTalking, getcharid(3);
		viewpoint 0,199,271,1,0xFF0000;
		set 'safAnnounceDelay, -1;
		set 'safReadyToWarp, 0;
		
		cutin "fenrir_a", 2;
		instance_announce instance_id(), "[Fenrir] So you came! Good! I'm really glad of it.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Let's hurry up.", bc_map, 0x0000FF;
		sleep2 1500;

		enablenpc instance_npcname( "alasaff3", instance_id() );	
			
		cutin "1", 255;
		disablenpc strnpcinfo(3);	
		enablenpc instance_npcname( "safwarp#2", instance_id() );
		enablenpc instance_npcname( "safwarp#3", instance_id() );
	}
	end;
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@glast,199,271,5	script	Fenrir::alasaff3	4_F_FENRIR,5,5,{
	end;
	
OnTouch:
	if('talking == 0)
	{
		set 'safTalking, getcharid(3);
		set 'talking, 1;
		cutin "fenrir_a", 2;
		instance_announce instance_id(), "[Fenrir] Once again, I'm glad that you weren't late and decided to assist me.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] Let's split to find the Sentinel Breeze. Sarah will probably be after it.", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Fenrir] One last thing. If you change your mind, I opened a warp on our first meeting place. Take it to escape.", bc_map, 0x0000FF;
		sleep2 1500;		
		
		enablenpc instance_npcname( "alasaff4", instance_id() );
		enablenpc instance_npcname( "alasafs", instance_id() );
		enablenpc instance_npcname( "safwarp#4", instance_id() );		
		cutin "", 255;
		disablenpc strnpcinfo(3);
	}
	end;
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@glast,200,334,3	script	Sarah::alasafs	4_F_SARAH,5,5,{
	end;
	
OnDialog1:
	
	cutin "sarah_hero3", 2;
	instance_announce instance_id(), "[Sarah] It looks like an infinite time waiting after you...Fenris Fenrir!", bc_map, 0x0000FF;
	sleep2 1500;
	
	doevent instance_npcname( "alasaff4", instance_id() ) + "::OnDialog2";	
	end;
	
OnDialog3:
	
	cutin "sarah_hero3", 2;
	instance_announce instance_id(), "[Sarah] Come take it by yourself, if you can!", bc_map, 0x0000FF;
	sleep2 1500;
	
	doevent instance_npcname( "alasaff4", instance_id() ) + "::OnDialog4";	
	end;
	
OnWhiteImprison:
	
	//white imprison	
	specialeffect 802;
	cutin "sarah_hero3", 2;
	instance_announce instance_id(), "[Sarah] You can't stop me like that!", bc_map, 0x0000FF;
	
	doevent instance_npcname( "alasaff4", instance_id() ) + "::OnDialog5";	
	end;


OnSarahDialog:
	
	sleep2 1500;
	set .@continue, 1;
	
	switch('skillSummonDamageCount)
	{
		case 0:
		case 1:
		case 2:
		case 3:
		case 4:
		case 5:
			switch(rand(0,2))
			{
				case 0:
					instance_announce instance_id(), "[Sarah] It was sudden, Fenrir...But unsufficient!!", bc_map, 0xFF0000;
					break;
				case 1:
					instance_announce instance_id(), "[Sarah] Not bad...Not at all. But still too weak!", bc_map, 0xFF0000;
					break;
				case 2:
					instance_announce instance_id(), "[Sarah] Why don't you stop your childish magic, Fenrir?!", bc_map, 0xFF0000;
					break;
			}
			break;
		case 6:
			set .startSkillRange, 3;
			set .endSkillRange, 5;
			instance_announce instance_id(), "[Sarah] I won't be defeated like this, do you hear me?!", bc_map, 0xFF0000;
			break;
		case 7:
			instance_announce instance_id(), "[Sarah] Ah...Arh!!! I, Sarah Irene, won't lose!!", bc_map, 0xFF0000;
			break;
		case 8:
			instance_announce instance_id(), "[Sarah] Ugh! You're stronger than i predicted, Fenris!", bc_map, 0xFF0000;
			break;
		case 9:
			set .startSkillRange, 5;
			set .endSkillRange, 7;
			instance_announce instance_id(), "[Sarah] Argh!!! I'll kill you all!", bc_map, 0xFF0000;
			break;
		case 10:
			instance_announce instance_id(), "[Sarah] Urk, this canno't happens...I canno't lose!", bc_map, 0xFF0000;
			set .@continue, 0;			
			break;		
		case 11:
			instance_announce instance_id(), "[Sarah] Urgh...ARGHHHHH!!!!!!", bc_map, 0xFF0000;
			
			killmonster strnpcinfo(4), instance_npcname( "sarah_and_Fenrir", instance_id() ) + "::OnGuardianDied";
			killmonster strnpcinfo(4), instance_npcname( "sarah_and_Fenrir", instance_id() ) + "::OnSummonDied";

			disablenpc instance_npcname( "alasaff4", instance_id() );
			disablenpc instance_npcname( "alasafs", instance_id() );
			
			enablenpc instance_npcname( "alasaff5", instance_id() );
			enablenpc instance_npcname( "alasafs2", instance_id() );
			end;			
	}			
	
	switch('castedSkill)
	{
		case 0:
			specialeffect 772;
			break;
		case 1:
			specialeffect 802;
			specialeffect 800;			
			break;
		case 2:
			specialeffect 142;
			break;
		case 3:
			specialeffect 802;
			specialeffect 726;
			break;
		case 4:
			specialeffect 916;
			break;
		case 5:
			specialeffect 802;
			specialeffect 90;
			break;
		case 6:
			specialeffect 930;
			break;
		case 7:
			specialeffect 802;
			specialeffect 804;
			break;
	}
			
	sleep2 3000;
	if(.@continue == 1)
	{
		doevent instance_npcname( "alasaff4", instance_id() ) + "::OnCastSkill";
	}
	else if(.@continue == 0)
	{
		//RATHENA
		//setunitdata 'safBossId, 2, 100;
		//setunitdata 'safBossId, 9, 0x170000;
		//HERCULES
		setunitdata 'safBossId, UDT_HP, 100;

		doevent instance_npcname( "alasaff4", instance_id() ) + "::OnLastSkill";
	}
	end;
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@glast,187,336,5	script	Fenrir::alasaff4	4_F_FENRIR,5,5,{
	end;
	
OnTouch:
	if('talking == 0)
	{
		set 'safTalking, getcharid(3);
		set 'talking, 1;
		
		doevent instance_npcname( "alasafs", instance_id() ) + "::OnDialog1";
	}
	end;
	
OnDialog2:	
	
	cutin "fenrir_b", 2;
	instance_announce instance_id(), "[Fenrir] No need of useless word, Sarah. Hand me the Sentinel Breeze immediately!", bc_map, 0x0000FF;
	sleep2 1500;

	doevent instance_npcname( "alasafs", instance_id() ) + "::OnDialog3";
	end;
	
	
OnDialog4:	
	
	cutin "fenrir_b", 2;
	instance_announce instance_id(), "[Fenrir] Sarah may be a bit to tough to face her alone. I'll fight her myself!", bc_map, 0x0000FF;
	sleep2 1500;
	instance_announce instance_id(), "[Fenrir] Pure white, unbreakable prison! Come down to my word now", bc_map, 0x0000FF;
	sleep2 1500;
	instance_announce instance_id(), "[Fenrir] White imprison!", bc_map, 0x0000FF;
	sleep2 1500;
	doevent instance_npcname( "alasafs", instance_id() ) + "::OnWhiteImprison";
	cutin "", 255;
	end;
	
OnDialog5:	
	
	instance_announce instance_id(), "[Fenrir] I've weakened her now. But she's not down, far from it! I'll fight her while she's shackled.", bc_map, 0x0000FF;
	sleep2 1500;
	instance_announce instance_id(), "[Fenrir] I'll need your help to handle the monsters she'll send over us. I'm relying on you, now!", bc_map, 0x0000FF;
	sleep2 1500;
	instance_announce instance_id(), "[Fenrir] Our goal is to attack Sarah when she'll weakened!", bc_map, 0x0000FF;
	sleep2 1500;
	cutin "", 255;
	charcommand "#me " + strcharinfo(0) + " hehe! Powerfull enemies make my blood boil!";

	hideonnpc instance_npcname( "alasafs", instance_id() );	

	doevent instance_npcname( "sarah_and_Fenrir", instance_id() ) + "::OnLastFightStart";
	
	set 'skillSummonDamageCount, 0;

	set 'castedSkill, -1;

	set 'fenGID, getnpcid(0, instance_npcname( "alasaff4", instance_id() ));	
	goto OnCastSkill;	
	end;

OnLastSkillCast:
	
	instance_announce instance_id(), "[Fenrir] Move! Now!", bc_map, 0x0000FF;
	sleep2 1500;
	instance_announce instance_id(), "[Fenrir] I summon you, Tetra Vortex!", bc_map, 0x0000FF;	
	set 'skillSummonDamageCount, 11;
	doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
	end;

OnLastSkill:
	
	instance_announce instance_id(), "[Fenris] Now! I've put Sarah on a critical condition, but I'm almost  out of magic! You have to finish her!", bc_map, 0x0000FF;
	sleep2 1500;
	instance_announce instance_id(), "[Fenris] Since you may not be able to finish her, I'll prepare a last skill!", bc_map, 0x0000FF;
	sleep2 1500;	
	set 'lastSkillCast, 0;
OnCastSkill:
	
	set 'castedSkill, 'castedSkill + 1;	
	
	switch('castedSkill)
	{
		case 0:
			//Poison Cloud
			//specialeffect cast;
			specialeffect 513;
			//progressbar "008800",6;
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] Lethal cloud, appear before me, and bring my ennemies a painfull death!", bc_map, 0x0000FF;
			sleep2 2500;
			instance_announce instance_id(), "[Fenrir] Cloud Kill!!", bc_map, 0x0000FF;			
			sleep2 2000;
			set 'skillSummonDamageCount, 'skillSummonDamageCount + 1;
			doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
			break;
		case 1:
			//Hell Fire
			//specialeffect cast;
			specialeffect 513;
			//progressbar "008800",6;
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] I speak to you and controls Fire. I, Fenris, desire a fire that does not smoulder!", bc_map, 0x0000FF;
			sleep2 2500;
			instance_announce instance_id(), "[Fenrir] Emerge! Hell Fire!!", bc_map, 0x0000FF;			
			sleep2 2000;
			set 'skillSummonDamageCount, 'skillSummonDamageCount + 1;
			doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
			break;
		case 2:
			//Heaven's Drive
			//specialeffect cast;
			specialeffect 513;
			//progressbar "008800",6;
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] The root of earthshaking power! Fear the power of the Earth itself!", bc_map, 0x0000FF;
			sleep2 2500;
			instance_announce instance_id(), "[Fenrir] Go forth! Heaven's Drive!!", bc_map, 0x0000FF;			
			sleep2 2000;
			set 'skillSummonDamageCount, 'skillSummonDamageCount + 1;
			doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
			break;
		case 3:
			//Frost Misty
			//specialeffect cast;
			specialeffect 513;
			specialeffect 910;
			//progressbar "008800",9;
			sleep2 1000;
			instance_announce instance_id(), "[Fenrir] Mercyless cold breath of the Northern wind...", bc_map, 0x0000FF;
			sleep2 3500;
			instance_announce instance_id(), "[Fenrir] Come down to earth, like the silent dawn mist, and freeze everything's breating on your path!", bc_map, 0x0000FF;
			sleep2 2500;
			specialeffect 910;
			instance_announce instance_id(), "[Fenrir] Frost Misty! I call you!!", bc_map, 0x0000FF;			
			sleep2 2000;
			set 'skillSummonDamageCount, 'skillSummonDamageCount + 2;
			doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
			break;
		case 4:
			//Demonic Fire
			//specialeffect cast;
			specialeffect 513;
			specialeffect 909;
			//progressbar "008800",7;
			sleep2 1000;
			instance_announce instance_id(), "[Fenrir] Endlessly burning conflagration, a fire that can scorch even the Soul. Come down to me, now!", bc_map, 0x0000FF;
			sleep2 3500;
			instance_announce instance_id(), "[Fenrir] Demonic Fire! I call you!!", bc_map, 0x0000FF;			
			sleep2 2500;
			specialeffect 909;
			set 'skillSummonDamageCount, 'skillSummonDamageCount + 2;
			doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
			break;
		case 5:
			//Thunderbolt
			//specialeffect cast;
			specialeffect 513;
			specialeffect 908;
			//progressbar "008800",7;
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] True power of the endless sky's wrath, come down to me now!", bc_map, 0x0000FF;
			sleep2 3500;
			instance_announce instance_id(), "[Fenrir] Lord of Vermillion! Fall now!!", bc_map, 0x0000FF;			
			sleep2 2000;
			set 'skillSummonDamageCount, 'skillSummonDamageCount + 2;
			doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
			break;
		case 6:
			//Varetyr Spear
			//specialeffect cast;
			specialeffect 513;
			specialeffect 885;
			//progressbar "008800",10;
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] Heaven's Sword, that pierce the sky!", bc_map, 0x0000FF;
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] Let me borrow your strength for a moment!", bc_map, 0x0000FF;			
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] Sword, that can even tear the Vacuum in pieces!", bc_map, 0x0000FF;			
			sleep2 1500;
			instance_announce instance_id(), "[Fenrir] Let down your endless force!", bc_map, 0x0000FF;			
			sleep2 4000;
			instance_announce instance_id(), "[Fenrir] I summon you, Varetyr Spear!", bc_map, 0x0000FF;	
			set 'skillSummonDamageCount, 'skillSummonDamageCount + 1;
			doevent instance_npcname( "alasafs", instance_id() ) + "::OnSarahDialog";
			break;
		case 7:
			//Tetra Vortex
			//specialeffect cast;
			specialeffect 513;
			specialeffect 805;
			//progressbar "008800",17;
			sleep2 2000;
			instance_announce instance_id(), "[Fenrir] Elemental Powers, Pride of Earth, Liberty of Wind, Wrath of Fire, Mercy of Water!", bc_map, 0x0000FF;
			sleep2 2500;
			if('safBossAlive == 1)
			{
				instance_announce instance_id(), "[Fenrir] From all directions, gathering your fierce damaging strength...", bc_map, 0x0000FF;			
				sleep2 2500;
				if('safBossAlive == 1)
				{
					instance_announce instance_id(), "[Fenrir] From the deepest Cold...", bc_map, 0x0000FF;	
					sleep2 2500;
					if('safBossAlive == 1)
					{
						instance_announce instance_id(), "[Fenrir] To the hotest Warmth...", bc_map, 0x0000FF;		
						sleep2 2500;
						if('safBossAlive == 1)
						{
							instance_announce instance_id(), "[Fenrir] Resisting to the fierest Breeze...", bc_map, 0x0000FF;		
							sleep2 2500;
							if('safBossAlive == 1)
							{
								instance_announce instance_id(), "[Fenrir] But defiling against the hardest Stones...", bc_map, 0x0000FF;		
								sleep2 2500;
								if('safBossAlive == 1)
								{
									instance_announce instance_id(), "[Fenrir] Raise, raise and hit, from all your rage...", bc_map, 0x0000FF;					
									set 'skillSummonDamageCount, 'skillSummonDamageCount + 1;
								}
							}
						}
					}
				}
			}
			break;
	}
	end;	
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@glast,200,348,4	script	Sarah::alasafs2	4_F_SARAH,5,5,{
	end;
	
OnTouch:
	if('talking == 0)
	{
		set 'safTalking, getcharid(3);
		set 'talking, 1;
		cutin "sarah_hero3", 2;
		
		instance_announce instance_id(), "[Sarah] Urk!", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Sarah] Not yet! I'm not defeated yet!", bc_map, 0x0000FF;
		sleep2 1500;
		instance_announce instance_id(), "[Sarah] You escaped, like a rat!", bc_map, 0x0000FF;
		sleep2 1500;		
		instance_announce instance_id(), "[Sarah] But I'm can't leave like that. I can let you go away like that!", bc_map, 0x0000FF;
		sleep2 1500;		
		instance_announce instance_id(), "[Sarah] I'll blow this entire place away!", bc_map, 0x0000FF;
		sleep2 1500;	
		specialeffect 563;
		doevent instance_npcname( "alasaff5", instance_id() ) + "::OnDialog2_1";
	}
	end;
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@glast,198,343,0	script	Fenrir::alasaff5	4_F_FENRIR,{
	end;
	
OnDialog2_1:	
	
	cutin "fenrir_a", 2;

	disablenpc instance_npcname( "alasafs2", instance_id() );
	
	instance_announce instance_id(), "[Fenrir] She's planning to destroy the entire castle!", bc_map, 0x0000FF;
	sleep2 1500;	
	instance_announce instance_id(), "[Fenrir] Run! Go back to the entrance!", bc_map, 0x0000FF;
	sleep2 1500;	
	
	cutin "", 255;
	set 'lastTimer, 0;
	
	//apply mapflags
	setmapflag .@map$,mf_nomobloot;
	setmapflag .@map$,mf_nomvploot;

	disablenpc strnpcinfo(3);
	enablenpc instance_npcname( "safwarp#5", instance_id() );
	enablenpc instance_npcname( "alasaff6", instance_id() );
	end;
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	disablenpc strnpcinfo(3);
	end;
}


1@glast,365,306,5	script	Fenrir::alasaff6	4_F_FENRIR,5,5,{
	end;
	
OnTouch:	
	if('talking == 0)
	{
		set 'safTalking, getcharid(3);
		cutin "fenrir_a", 2;
		
		instance_announce instance_id(), "[Sarah] You're finally here! Get ready, I'll warp us all!", bc_map, 0x0000FF;
		sleep2 1500;	
		cutin "", 255;

		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && strnpcinfo(4) == strcharinfo(3)){
				//getreward
				getitem 607, 1;
				warp "dali02",92,145;
				set SARAFENRIR, 1;
			}
		
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && SAF_INSTANCE_ID > 0){
				set SAF_INSTANCE_ID, -1;
			}
		instance_destroy instance_id();
	}
	end;
	
OnInstanceInit:
	set 'talking, 0;
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@glast,351,275,5	warp	safwarp#0	2,2,1@glast,351,260
1@glast,351,270,5	warp	safwarp#02	2,2,1@glast,351,280
//first exit one
1@glast,375,304,5	warp	safwarp#1	2,2,dali02,97,142
//enter last floor
1@glast,200,235,5	warp	safwarp#2	2,2,1@glast,200,265
//back from last floor
1@glast,200,260,5	warp	safwarp#3	2,2,1@glast,200,229
//enter last fight area
1@glast,200,296,5	warp	safwarp#4	2,2,1@glast,199,331
//leave last fight area
1@glast,200,326,5	warp	safwarp#5	2,2,1@glast,200,290
