//===== Alayne Scripts =======================================
//= Last Room Instance
//===== By: ================================================== 
//= Alayne
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//= hercules SVN
//===== Description: ========================================= 
//= The official instance Last Room transcription
//===== Todo Remaining: ======================================
//
//===== Additional Comments: =================================
//
//
//============================================================

un_myst,163,38,5	script	Marik::alalrma	4_M_BIRMAN,{
OnStart:
	set .@party_id, getcharid(1);
	set .@lr_leader, ( getcharid(0) == getpartyleader(.@party_id,2) );
	
	set .@has_instance, LR_INSTANCE_ID > 0;
	
	set .@instance_delay, ( instance_delay_lastr - gettimetick(2) );

	set .@npcname$, "[Marik]";
	mes .@npcname$;
	mes "Unlike the other doors we saw, this door has traces of it being blocked from this side";
	next; 
	mes .@npcname$;
	mes "I can't believe you want to enter a place like this again...";
	mes "Is there anything in there?";
	next;
	switch( select( 
		(( .@party_id && .@lr_leader && !.@has_instance )?"Create " + .instance_name$:""),
		(( .@party_id && .@has_instance )?"Enter " + .instance_name$:""),
		(( .@party_id && .@lr_leader && .@has_instance )?"Destroy " + .instance_name$:""),
	"Nevermind."
		)){
	case 1:
		if( .@instance_delay > 0 )
		{
			mes "You have to wait : ^FF0000"+.@instance_delay+" Seconds.^000000";
			close;
		}
		getpartymember .@party_id,2;
		if( .min_party_member >= 1 ){
			set .@origin, getcharid(3);
			set .@gettimetick, gettimetick(2);
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set .@member_count, .@member_count + 1;
					if( BaseLevel < .base_level[0] || BaseLevel > .base_level[1] ) set .@fail, .@fail|1;
					if( Zeny < .instance_zeny ) set .@fail, .@fail|2;
					if( instance_delay_lastr > .@gettimetick ) set .@fail, .@fail|4;
					if( .@fail ){
						set .@name$, strcharinfo(0);
						break;
					}
				}
			attachrid( .@origin );
		}
		
		//clean any previously remaining instance
		if(LR_INSTANCE_ID != -1)
		{
			instance_destroy instance_id();
			getpartymember getcharid(1),2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set LR_INSTANCE_ID, -1;
				}
		}
		
		if( .@name$ != "" && .@fail ){
			mes "^FF0000[ Failed ]^000000";
			mes "^0055FF"+.@name$+"^000000, You cannot join right now.";
			mes " ";
			mes "^FF0000[ Reason: ]^000000";
			if( .@fail & 1 ) mes "^777777You must be Level: "+.base_level[0]+" - "+.base_level[1]+"^000000";
			if( .@fail & 2 ) mes "^777777You need "+ .instance_zeny +" Zeny^000000";
			if( .@fail & 4 ) mes "^777777You still have cooldown.^000000";
			break;
		}
		
		getpartymember getcharid(1),0;
		if (instance_check_party(.@party_id, .min_party_member, .base_level[0], .base_level[1]) == 0) {
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "Make or join a party with at least "+.min_party_member+" member and try again, with level between " +.base_level[0] + " and " + .base_level[1] + ".";
		}
		else{
			//RATHENA
			//set .@instance, instance_create( .instance_name$ );
			//HERCULES
			set .@instance, instance_create(.instance_name$, .@party_id, IOT_PARTY);
			if( .@instance < 0 ) {
				npctalk .instance_name$ + " reservation failed.";
				mes "^0000FF"+ .instance_name$ +" ^000000 - Reservation Failed!";
				close;
			}
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "^0000FF"+.instance_name$+" ^000000 - Reserved";
			set .@gettimetick, gettimetick(2);
			getpartymember .@party_id,2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set instance_delay_lastr, ( .@gettimetick + ( .instance_delay ));
					set Zeny, Zeny - .instance_zeny;
					set LR_INSTANCE_ID, .@instance;
					dispbottom "["+.instance_name$+"] -"+ .instance_zeny +" Zeny and Instance Cooldown Applied.";
				}
			attachrid( .@origin );
			npctalk .instance_name$ + " has been generated for Party - " + getpartyname( .@party_id );
			
			//HERCULES
			for( set .@i, 0; .@i < getarraysize(.map$); set .@i,.@i+1 )
			{
				if( instance_attachmap( .map$[.@i],.@instance ) == "" ){
					setmapflag .map$[.@i],mf_zone,"Memorial Dungeon";
					npctalk .instance_name$ + " reservation failed due to map attach failure.";
					mes "- ^FF0000Reservation Failed!^000000";
					instance_destroy( instance_id() );
					close;
				}
			}
			instance_set_timeout .instance_delay,300,instance_id();
			instance_init(instance_id());
			
			//disable unused npcs for now
			disablenpc instance_npcname( "lrwarp#0", instance_id() );
			disablenpc instance_npcname( "lrwarp#1", instance_id() );
 		}
	case 2:
		if( LR_INSTANCE_ID <= 0 ){
			mes .instance_name$ + " doesn't exist for Party - "+getpartyname( .@party_id );
		}
		else{
			getmapxy(.@map$, .@x, .@y, UNITTYPE_PC);
			//RATHENA
			//save .@map$, .@x, .@y;
			//switch(instance_enter(.instance_name$)) {
			//	default:
			//		mes "An unknown error has occurred.";
			//		close;
			//	case 2:
			//		mes "The memorial dungeon " + .instance_name$ + " does not exist.";
			//		mes "The party leader did not generate the dungeon yet.";
			//		close;
			//	case 1:
			//		mes "You can enter the dungeon after making the party.";
			//		close;
			//	case 0:
			//		mapannounce .map$[0], strcharinfo(0)+" from party, " +getpartyname( .@party_id )+", lr entering the dungeon, "+.instance_name$+".",bc_map,"0x00ff99",FW_NORMAL,12;
			//		end;
			//}
			
			//HERCULES
			savepoint .@map$, .@x, .@y;
			if( has_instance( .map$[0] ) == "" ){
				mes "Last Room doesnt exist for Party - "+getpartyname( .@party_id );
			}
			else{
				warp .map$[0],141,24;
				instance_announce instance_id(), getpartyname(getcharid(1))+"  entered in the "+.instance_name$+"!",bc_map,"0x00ff99";
			}
		}
		break;
	case 3:
		mes .instance_name$ + " Destroyed.";
		instance_destroy instance_id();
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) ){
				set LR_INSTANCE_ID, -1;
			}
		break;
	default: 
		break;
}
close;

OnPCLoginEvent:
OnPCLogoutEvent:
	set LR_INSTANCE_ID, -1;
	end;

OnInit:
	setarray .base_level[0],130, 175;
	setarray .map$[0],"1@uns";
	set .map_size, getarraysize( .map$ );
	set .min_party_member, 1;
	set .instance_zeny, 0;
	set .instance_delay, 7200;
	set $lr_timer_delay, 3600; //1 hour to clean the instance
	set .instance_name$, "Last Room";
	end;
}


1@uns,101,107,4	script	last_room	HIDDEN_WARP_NPC,2,2,{
function summon_normal;
function summon_guardian;
end;

OnInstanceInit:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
	
	set 'instance_lr_level, 0;
	set .max_lr_level, 3;
	
	set .bossId, -1;
	deletearray .systemIds[0], getarraysize(.systemIds);
	
	set .timerCount, 0;
	initnpctimer;
	end;

function	summon_normal	{
	set .@map$, getarg(0);
	set .@npcname$, getarg(1);
	set .@increaseFloor, getarg(2);
	
	if(.@increaseFloor == 1)
	{
		'instance_lr_level++;
	}
	
	switch( 'instance_lr_level ){
		case 1:
			//monsters + system message
			setarray .@monster,3251,3252,3251,3252,3253;
			setarray .@amount,20,5,20,5,1;
			setarray .@coordX[0], 145, 63, 145, 63, 26, 101, 26, 101, 30, 47, 30, 47; //2 x for each mob to define an area
			setarray .@coordY[0], 48, 78, 48, 78, 100, 123, 100, 123, 47, 39, 47, 39; //2 x for each mob to define an area
			break;
		case 2:
			//monsters
			setarray .@monster,3251,3252,3251,3252,3251,3252;
			setarray .@amount,20,10,20,10,20,10;
			setarray .@coordX[0], 143, 225, 143, 225, 229, 178, 229, 178, 184, 225, 184, 225; //2 x for each mob to define an area
			setarray .@coordY[0], 107, 84, 107, 84, 65, 52, 65, 52, 38, 26, 38, 26; //2 x for each mob to define an area
			break;
		case 3:
			//system message
			setarray .@monster,3253;
			setarray .@amount,10;
			setarray .@coordinate,226,223,166,187;
			break;
		default:
			attachrid('lrTalking);
			mapannounce .@map$, "[ Last Room ] : All Floors has been cleared, congratulation. Champions Arena close in 30 seconds.",bc_map,"0x00FF99";
			sleep2 15000;
			instance_destroy instance_id();
			getpartymember getcharid(1),2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set LR_INSTANCE_ID, -1;
				}
			return;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		set .@target_amount, ( .@amount[.@i] );
		set .@size, 0;
		if(getarraysize(.@label$) == 0)
		{
			set .@clabel$, .@npcname$ + "::OnNormalDied";
		}
		else
		{
			set .@clabel$, .@npcname$ + "::" + .@label$[.@i];
		}	
		if(getarraysize(.@coordX) > 0)
		{
			set .@coordinate[0], .@coordX[.@i * 2];
			set .@coordinate[1], .@coordY[.@i * 2];
			set .@coordinate[2], .@coordX[.@i * 2 + 1];
			set .@coordinate[3], .@coordY[.@i * 2 + 1];
		}
		if('instance_lr_level != 3)
		{
			areamonster .@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@clabel$,.@size;
		}
		else
		{
			set .@mob_dead_num, mobcount(.@map$,.@npcname$+"::OnGuardianDied" );
			if(.@mob_dead_num > 0)
			{
				for(set .@j,0; .@j<.@target_amount; set .@j,.@j+1)
				{
					//RATHENA
					//areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],1,.@clabel$,.@size);
					//set .systemIds[.@j], $@mobId[0];
					//HERCULES
					set .systemIds[.@j], areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],1,.@clabel$,.@size);
				}
			}
		}
	}
	return;
}

function	summon_guardian	{
	set .@map$, getarg(0);
	set .@label$, getarg(1);
	
	setarray .@coordinate,216,225,216,225;
			
	switch( 'instance_lr_level ){
		case 3:
			//T_W_O
			setarray .@monster,3254;
			break;
		default: 
			mapannounce .@map$,"[ Last Room ] : Cleared!! Congratulation!!",bc_map,0x00FF99;
			return;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		set .@target_amount, 1;
		set .@size, 0;
		//HERCULES
		set .bossId, areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
		//RATHENA	
		//areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
		//set .bossId, $@mobId[0];
	}
	return;
}

OnNormalDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
	if( .@mob_dead_num == 0 && .@mob_dead_num2 == 0 ){		
		if('instance_lr_level == 1)
		{
			enablenpc instance_npcname( "alalrta2", instance_id() );
		}
		else if('instance_lr_level == 2)
		{
			enablenpc instance_npcname( "alalrve2", instance_id() );
		}
	}
	end;
	
OnGuardianDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnGuardianDied" );
	set .@party_id, getcharid(1);
	
	if( !.@mob_dead_num ){
		attachrid('lrTalking);
		if ('instance_lr_level == .max_lr_level)
		{			
			killmonsterall .@map$, "All";
			
			enablenpc instance_npcname( "alalrve4", instance_id() );
			enablenpc instance_npcname( "alalrdu2", instance_id() );
			enablenpc instance_npcname( "alalrta3", instance_id() );
			enablenpc instance_npcname( "alalral", instance_id() );
			enablenpc instance_npcname( "alalrma2", instance_id() );
			end;
		}
		
		sleep2 15000;
		summon_normal( .@map$,.@npc_name$ );
	}
	end;
	
OnTimer10000:
	//each 10s, increase timer
	set .timerCount, .timerCount + 10;
	if(.timerCount == $lr_timer_delay)
	{
		attachrid('lrTalking);
		instance_announce instance_id(), "[ Last Room ] The Dimension collapse! I have to get you out of there!", bc_map, 0xFF0000;
		sleep2 15000;
		instance_destroy instance_id();
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) ){
				set LR_INSTANCE_ID, -1;
			}
		stopnpctimer;
	}
	if(.randomPop == 1)
	{
		set .@map$, strnpcinfo(4);
		set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
		set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
		if(.@mob_dead_num == 0)
		{
			summon_normal( .@map$, .@npc_name$, 0 );
		}
		else
		{
			for(set .@i,0; .@i<getarraysize(.systemIds); set .@i,.@i+1)
			{
				unitskilluseid .systemIds[.@i], 2051, 10, .bossId;
				set .@rand, .@rand - 1;
			}
		}
	}
	setnpctimer 0;
	end;
	
OnFirstFloorOn:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
	//pop monsters
	summon_normal( .@map$,.@npc_name$, 1 );
	end;
	
OnSecondFloorOn:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
	//pop monsters
	summon_normal( .@map$,.@npc_name$, 1 );
	end;
	
OnThirdFloorOn:
	'instance_lr_level++;
	set .randomPop, 1;
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), instance_id() );
	//pop boss
	summon_guardian( .@map$,.@npc_name$ + "::OnGuardianDied" );
	end;
}

1@uns,139,32,5	script	Du::alalrdu	4_M_REDMAN,{
	end;
		
OnTalk:			
	attachrid('lrTalking);
	cutin "bu_du5", 2;
	instance_announce instance_id(), "[Du] Sister are you allright?", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrve", instance_id() ) + "::OnTalk2";
	end;
	
OnTalk2:		
	attachrid('lrTalking);	
	cutin "bu_du1", 2;
	cutin "bu_du1", 2;
	instance_announce instance_id(), "[Du] Eifie! I think it is better that you go back!", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrve", instance_id() ) + "::OnTalk4";
	end;
}

1@uns,141,34,3	script	Verity::alalrve	4_F_BERRYTEA,2,2,{
		end;
		
OnTouch:	
	if(.talking == 0)
	{
		set 'lrTalking, getcharid(3);
		set .talking, 1;	

		cutin "ep15_2_brt_3", 2;
		instance_announce instance_id(), "[Verity] Humpf. I suddenly have a feeling that is no good...", bc_map, 0x00FF00;
		sleep2 1500;
	
		instance_announce instance_id(), "[Verity] Something is splitting my head...Ugh...", bc_map, 0x00FF00;
		sleep2 1500;

		instance_announce instance_id(), "[Verity] Ugh", bc_map, 0x00FF00;
		sleep2 1500;
	
		donpcevent instance_npcname( "alalrdu", instance_id() ) + "::OnTalk";
	}
	end;
	
OnTalk2:
	attachrid('lrTalking);
	cutin "ep15_2_brt_3", 2;
	instance_announce instance_id(), "[Verity] Have to go back", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrta", instance_id() ) + "::OnTalk";
	end;	

OnTalk3:
	attachrid('lrTalking);
	cutin "ep15_2_brt_4", 2;
	instance_announce instance_id(), "[Verity] Ugh", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrdu", instance_id() ) + "::OnTalk2";
	end;		
	
OnTalk4:
	attachrid('lrTalking);
	cutin "ep15_2_brt_5", 2;
	instance_announce instance_id(), "[Verity] N...No. I will catch up soone so go on ahead", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrta", instance_id() ) + "::OnTalk2";
	end;	

OnInstanceInit:
	setcell strnpcinfo(4),138,45,143,45,cell_walkable,0;
	set .talking, 0;
	end;
}

1@uns,144,33,3	script	Tamarin::alalrta	4_M_TAMARIN,{ //10027
		end;
		
OnTalk:			
	attachrid('lrTalking);
	cutin "ep143_tahuk", 2;
	instance_announce instance_id(), "[Tamarin] Verity are you allright?", bc_map, 0x00FF00;
	sleep2 1500;

	instance_announce instance_id(), "[Tamarin] Can you walk?", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrve", instance_id() ) + "::OnTalk3";
	end;
	
OnTalk2:	
	attachrid('lrTalking);
	cutin "ep143_tasmi", 2;
	instance_announce instance_id(), "[Tamarin] Then i'll stay with Verity until she recovers. So you can go on!", bc_map, 0x00FF00;
	sleep2 1500;

	cutin "", 255;
	
	disablenpc instance_npcname( "alalrve", instance_id() );
	disablenpc instance_npcname( "alalrdu", instance_id() );
	disablenpc instance_npcname( "alalrta", instance_id() );
	
	enablenpc instance_npcname( "lrwarp#0", instance_id() );
	enablenpc instance_npcname( "alalrse", instance_id() );
	end;
}

1@uns,67,97,3	script	Tamarin::alalrta2	4_M_TAMARIN,2,2,{
	end;

OnTouch:
	if(.talking == 0)
	{
		cutin "ep143_tahuk", 2;
		set .talking, 1;

		instance_announce instance_id(), "[Tamarin] Huff huff! Verity suddenly huff huff couldn't event stand straight before huff huff...", bc_map, 0x00FF00;
		sleep2 1500;
		
		instance_announce instance_id(), "[Tamarin] Ran off so fast huff huff...to even catch up! huff huff", bc_map, 0x00FF00;
		sleep2 1500;
		
		instance_announce instance_id(), "[Tamarin] Uh...Could Verity have gone that far?", bc_map, 0x00FF00;
		sleep2 1500;
		
		instance_announce instance_id(), "[Tamarin] We need to find her quickly!", bc_map, 0x00FF00;
		sleep2 1500;
		
		instance_announce instance_id(), "[Tamarin] I have a bad feeling about that", bc_map, 0x00FF00;
		sleep2 1500;

		cutin "", 255;

		disablenpc strnpcinfo(3);
		enablenpc instance_npcname( "lrwarp#1", instance_id() );
		enablenpc instance_npcname( "alalrse2", instance_id() );
		enablenpc instance_npcname( "alalrse3", instance_id() );
		enablenpc instance_npcname( "alalrse4", instance_id() );
		enablenpc instance_npcname( "alalrse4", instance_id() );
		enablenpc instance_npcname( "alalrse5", instance_id() );
		enablenpc instance_npcname( "alalrse7", instance_id() );
		enablenpc instance_npcname( "alalrse8", instance_id() );
		enablenpc instance_npcname( "alalrse9", instance_id() );
		enablenpc instance_npcname( "alalrse10", instance_id() );
	}
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	set .talking, 0;
	end;
}

1@uns,135,54,3	script	Security System::alalrse	HIDDEN_WARP_NPC,5,5,{
	end;
	
OnTouch:	
	if('instance_lr_level == 0)
	{
		instance_announce instance_id(), "[Security System] Intruder detected on area 1. Protection system activated.", bc_map, 0xFF0000;
		donpcevent instance_npcname( "last_room", instance_id() ) + "::OnFirstFloorOn";
	}
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}


1@uns,149,104,3	script	Security System::alalrse2	HIDDEN_WARP_NPC,5,5,{
	end;
	
OnTouch:	
	specialeffect 812;
	instance_announce instance_id(), "[Security System] Intruder detected on area 2. Increased protection system activated.", bc_map, 0xFF0000;
		
	if('instance_lr_level == 1)
	{
		donpcevent instance_npcname( "last_room", instance_id() ) + "::OnSecondFloorOn";
	}
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@uns,155,97,3	duplicate(alalrse2)	Security System::alalrse3	HIDDEN_WARP_NPC,5,5
1@uns,162,96,3	duplicate(alalrse2)	Security System::alalrse4	HIDDEN_WARP_NPC,5,5
1@uns,170,96,3	duplicate(alalrse2)	Security System::alalrse5	HIDDEN_WARP_NPC,5,5
1@uns,179,96,3	duplicate(alalrse2)	Security System::alalrse6	HIDDEN_WARP_NPC,5,5
1@uns,187,97,3	duplicate(alalrse2)	Security System::alalrse7	HIDDEN_WARP_NPC,5,5
1@uns,195,95,3	duplicate(alalrse2)	Security System::alalrse8	HIDDEN_WARP_NPC,5,5
1@uns,203,97,3	duplicate(alalrse2)	Security System::alalrse9	HIDDEN_WARP_NPC,5,5
1@uns,209,91,3	duplicate(alalrse2)	Security System::alalrse10	HIDDEN_WARP_NPC,5,5

1@uns,226,30,3	script	Verity::alalrve2	4_F_BERRYTEA,{ //10078
	set .@npcname$, "[Verity]";
	mes .@npcname$;
	cutin "ep15_2_brt_5", 2;
	mes "Ah...Why am I in a place like this...What happened?";
	mes "My head...is splitting...";
	mes "A sound coming from this...door?";
	if(select("Quit","Go inside") == 2)
	{
		next;
		mes .@npcname$;
		mes "Quickly...We have to go back";
		cutin "", 255;
		warp strnpcinfo(4), 55, 168;
	}
	
	enablenpc instance_npcname( "alalrve3", instance_id() );
	close;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@uns,216,224,3	script	Verity::alalrve3	4_F_BERRYTEA,5,5,{	
	end;

OnTouch:
	if('instance_lr_level == 2 && .poping == 0)
	{	
		set .poping, 1;

		cutin "ep15_2_brt_7", 2;
		instance_announce instance_id(), "[Verity] Finally...We came back...", bc_map, 0x00FF00;
		sleep2 1500;
		
		instance_announce instance_id(), "[Verity] Finally...", bc_map, 0x00FF00;
		sleep2 1500;
		
		instance_announce instance_id(), "[Security System] Intruder in mother room!", bc_map, 0xFF0000;
		sleep2 1500;		
		instance_announce instance_id(), "[Security System] Starting emergency protocol!", bc_map, 0xFF0000;
	
		cutin "", 255;
		donpcevent instance_npcname( "last_room", instance_id() ) + "::OnThirdFloorOn";
		disablenpc strnpcinfo(3);
	}
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	set .poping, 0;
	end;
}

1@uns,216,224,3	script	Verity::alalrve4	4_F_BERRYTEA,{
	end;
	
OnTalk:	
	attachrid('lrTalking);
	cutin "ep15_2_brt_7", 2;
	instance_announce instance_id(), "[Verity] Uh...Uhm! What's going on?", bc_map, 0x00FF00;
	sleep2 1500;
	
	instance_announce instance_id(), "[Verity] With those serious faces!", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrdu2", instance_id() ) + "::OnTalk2";
	end;
	
OnTalk2:	
	attachrid('lrTalking);
	cutin "ep15_2_brt_7", 2;
	instance_announce instance_id(), "[Verity] Remember?", bc_map, 0x00FF00;
	sleep2 1500;
	
	instance_announce instance_id(), "[Verity] Now that I think of it...Where am I?", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrta3", instance_id() ) + "::OnTalk2";
	end;
	
OnTalk3:	
	attachrid('lrTalking);
	cutin "ep15_2_brt_3", 2;
	instance_announce instance_id(), "[Verity] Hum...I think my head was splitting since I went underground...", bc_map, 0x00FF00;
	sleep2 1500;
	
	instance_announce instance_id(), "[Verity] I don't remember anything after that...", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrdu2", instance_id() ) + "::OnTalk3";
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@uns,217,221,5	script	Du::alalrdu2	4_M_REDMAN,2,2,{ //628
	end;
	
OnTouch:
	if(.talking == 0)
	{
		set 'lrTalking, getcharid(3);
		cutin "bu_du1", 2;
		set .talking, 1;
		instance_announce instance_id(), "[Du] Sister! What happened? Are you allright?", bc_map, 0x00FF00;
		sleep2 1500;
		
		donpcevent instance_npcname( "alalrta3", instance_id() ) + "::OnTalk";
	}
	end;
	
OnTalk2:
	attachrid('lrTalking);
	cutin "bu_du5", 2;
	instance_announce instance_id(), "[Du] Do you remember? What did you think!", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrve4", instance_id() ) + "::OnTalk2";
	end;
	
OnTalk3:
	attachrid('lrTalking);
	cutin "bu_du1", 2;
	instance_announce instance_id(), "[Du] Just now something tremendous", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalral", instance_id() ) + "::OnTalk";
	end;
	
OnTalk4:
	attachrid('lrTalking);
	cutin "bu_du5", 2;
	instance_announce instance_id(), "[Du] You call this chatting away?", bc_map, 0x00FF00;
	sleep2 1500;
	
	instance_announce instance_id(), "[Du] It was tremendous before okay?", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalral", instance_id() ) + "::OnTalk2";
	end;
	
OnTalk5:
	attachrid('lrTalking);
	cutin "bu_du1", 2;
	instance_announce instance_id(), "[Du] What...What's this now?", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalral", instance_id() ) + "::OnTalk3";
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	set .talking, 0;
	end;
}

1@uns,211,223,5	script	Alp::alalral	4_M_ACROSS,{ //884
	end;
	
OnTalk:
	attachrid('lrTalking);
	cutin "bu_alp1", 2;
	instance_announce instance_id(), "[Alp]Hey! I don't think this is a situation we should be chatting away carefree now...", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrdu2", instance_id() ) + "::OnTalk4";
	end;
	
OnTalk2:
	attachrid('lrTalking);
	cutin "bu_alp3", 2;
	instance_announce instance_id(), "[Alp]Don't you hear? Something's coming...", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrta3", instance_id() ) + "::OnTalk3";
	end;
	
OnTalk3:
	attachrid('lrTalking);
	cutin "bu_alp5", 2;
	instance_announce instance_id(), "[Alp]This is dangerous...That's not something we can handle	", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrma2", instance_id() ) + "::OnTalk2";
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@uns,215,221,2	script	Tamarin::alalrta3	4_M_TAMARIN,{
	end;
	
OnTalk:
	attachrid('lrTalking);
	cutin "ep143_tasta", 2;
	instance_announce instance_id(), "[Tamarin] Verity! Pull yourself together!", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrma2", instance_id() ) + "::OnTalk";
	end;
	
OnTalk2:
	attachrid('lrTalking);
	cutin "ep143_taang", 2;
	instance_announce instance_id(), "[Tamarin] Verity, you don't remember a thing? Before...", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrve4", instance_id() ) + "::OnTalk3";
	end;
	
OnTalk3:
	attachrid('lrTalking);
	cutin "ep143_tahuk", 2;
	instance_announce instance_id(), "[Tamarin] Now...now that I look the ground is...shaking, isn't it?", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrdu2", instance_id() ) + "::OnTalk5";
	end;	
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@uns,220,227,0	script	Marik::alalrma2	4_M_BIRMAN,{ //10060
	end;
	
OnTalk:
	attachrid('lrTalking);
	cutin "bu_mark1", 2;
	instance_announce instance_id(), "[Marik] Everybody, out of the way! I'll cast Heal", bc_map, 0x00FF00;
	sleep2 1500;
	
	donpcevent instance_npcname( "alalrve4", instance_id() ) + "::OnTalk";
	end;
	
OnTalk2:
	attachrid('lrTalking);
	cutin "bu_mark4", 2;
	instance_announce instance_id(), "[Marik] Qu...Quickly! Outside", bc_map, 0x00FF00;
	sleep2 1500;

	cutin "", 255;
	
	getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && strcharinfo(3) == strnpcinfo(4)){
				warp "verus01", 148, 177;
			}
	
	sleep2 1500;
	instance_destroy instance_id();
	getpartymember getcharid(1),2;
	for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
		if( attachrid( $@partymemberaid[.@i] ) ){
			set LR_INSTANCE_ID, -1;
		}
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@uns,142,41	warp	lrwarp#0	2,2,1@uns,139,49
1@uns,98,123	warp	lrwarp#1	2,2,1@uns,144,112

